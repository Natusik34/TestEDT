

#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ИмяСхемыКомпоновкиДанных = "ТиповаяСхема" ; 
	КонецЕсли;
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Компания = Константы.УчетПоКомпании.Компания(Объект.Организация);
	НациональнаяВалюта = Константы.НациональнаяВалюта.Получить();
	
	ОбновитьРеквизитыВидимостиФормы();
	ОпределениеСхемСКДНаСервере();

	// Установка способа выбора структурной единицы в зависимости от ФО.
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям.Получить()
		И НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимСкладам.Получить() Тогда
		
		Элементы.СтруктурнаяЕдиница.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		Элементы.СтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		
	КонецЕсли;
	

	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	НапоминанияПользователяУНФ.УстановитьОтображениеКомандОрганайзера(Элементы);
	ЭлектроннаяПочтаУНФ.УстановитьОтображениеКомандОтправкиСообщений(Элементы);
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	Если ИспользоватьПодключаемоеОборудование Тогда
		ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
		ТипыОборудования.СканерШтрихкода = Истина;
		МенеджерОборудования.ПриСозданииНаСервере(ЭтотОбъект, ТипыОборудования);
	КонецЕсли;
	МенеджерОборудованияУНФ.УстановитьВидимостьЭлементовПоДоступномуПодключаемомуОборудованию(Элементы,
		"ЗапасыЗагрузитьДанныеИзТСД",
		"ЗапасыПолучитьВес");
	// Конец ПодключаемоеОборудование
	
	// КопированиеСтрокТабличныхЧастей
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Запасы");
	
	// Серии номенклатуры
	ИспользоватьСерииНоменклатурыОстатки = СерииНоменклатурыУНФ.ИспользоватьСерииНоменклатурыОстатки();
	
	// Характеристики
	НоменклатураВДокументахСервер.ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик(ЭтаФорма);
	Если Параметры.Ключ.Пустая()
		Тогда
		НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект, Истина)
	КонецЕсли;
	
	НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "ИнвентаризацияЗапасов", НастройкиФормыВыбораНоменклатуры);
	НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
	Элементы.ЗапасыГруппаСерииНоменклатуры.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	// ИнтеграцияГосИС
	СобытияФормИСУНФ.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// Конец ИнтеграцияГосИС
	
	НастройкиКоличества = ПолучитьНастройкиИзмененияКоличества();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиКоличества);
		
	УстановитьДоступностьТиповСтруктурныхЕдиниц();
	
		СхемаКомпановкиДанныхУсловия = Объект.Ссылка.ХранилищеСхемыКомпоновкиДанных.Получить();
		НастройкиСКД = Объект.Ссылка.ХранилищеНастроекКомпоновкиДанных.Получить();		
		Если СхемаКомпановкиДанныхУсловия = Неопределено Тогда
			// Схема не выбрана. Обнулим схему. 
			Объект.ИмяСхемыКомпоновкиДанных = "";			
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.Ссылка) и не СхемаКомпановкиДанныхУсловия = Неопределено Тогда
		ОбработатьИзменениеСхемыСКД(СхемаКомпановкиДанныхУсловия, НастройкиСКД); 
		КонецЕсли;
	//Конецесли;
	
	// ГрупповоеИзменениеСтрок
	ЗаполнитьСписокДействий();
	ГрупповоеИзменениеСтрокСервер.ПриСозданииНаСервере(НаборЭлементовГрупповогоИзмененияСтрокСервер(), ЗапасыИзменениеСтрокДействие);
	ЗапасыИзменениеСтрокДействиеПриОткрытии = ЗапасыИзменениеСтрокДействие;
	//ВыделитьЭлементы(Истина);
	// Конец ГрупповоеИзменениеСтрок
	
КонецПроцедуры // ПриСозданииНаСервере()

// Серверная часть процедура УсловиеПредоставленияПриИзменении - обработчика события ПриИзменении элемента
// УсловиеПредоставления формы.
//
&НаСервере
Процедура ОпределениеСхемСКДНаСервере()
	
	
		Если ЗначениеЗаполнено(Объект.ИмяСхемыКомпоновкиДанных) Тогда			
			СхемаКомпоновкиДанныхПриИзмененииНаСервере(Ложь);
		КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеСхемыСКД(СхемаКомпановкиДанныхУсловия, НастройкиДоИзменеиня = Неопределено)
	Если ЗначениеЗаполнено(АдресСхемыКомпановкиДанных) Тогда
		ПоместитьВоВременноеХранилище(СхемаКомпановкиДанныхУсловия, АдресСхемыКомпановкиДанных);
	Иначе
		АдресСхемыКомпановкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпановкиДанныхУсловия, УникальныйИдентификатор);
	КонецЕсли;
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпановкиДанных);
	НастройкаКомпановкиДанных.Инициализировать(ИсточникНастроек);
	Если ЗначениеЗаполнено(НастройкиДоИзменеиня) Тогда
		НастройкаКомпановкиДанных.ЗагрузитьНастройки(НастройкиДоИзменеиня);
		НастройкаКомпановкиДанных.Восстановить();
	Иначе
		НастройкаКомпановкиДанных.ЗагрузитьНастройки(СхемаКомпановкиДанныхУсловия.НастройкиПоУмолчанию);  
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Организация");
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение = Объект.Организация;
		КонецЕсли;
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("СтруктурнаяЕдиница");
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение = Объект.СтруктурнаяЕдиница;
		КонецЕсли;
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Период");
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение = Объект.Дата;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СхемаКомпоновкиДанныхПриИзмененииНаСервере(СохранитьНастройки = Истина)
	НастройкиДоИзменения = Неопределено;
	Если СохранитьНастройки Тогда
		НастройкиДоИзменения = НастройкаКомпановкиДанных.ПолучитьНастройки();
	КонецЕсли;
	СхемаКомпановкиДанныхУсловия = Неопределено;
	Попытка
		СхемаКомпановкиДанныхУсловия = Документы.ИнвентаризацияЗапасов.ПолучитьМакет(Объект.ИмяСхемыКомпоновкиДанных);		
		Объект.СхемаСКДМодифицированаВручную = Ложь;
	Исключение
		СхемаКомпановкиДанныхУсловия = Неопределено;
	КонецПопытки;
	Если Не СхемаКомпановкиДанныхУсловия = Неопределено Тогда
		ОбработатьИзменениеСхемыСКД(СхемаКомпановкиДанныхУсловия, НастройкиДоИзменения);
	КонецЕсли;
КонецПроцедуры


// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
	// ИнтеграцияГосИС
	СобытияФормИСУНФ.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец ИнтеграцияГосИС
	
КонецПроцедуры // ПриЧтенииНаСервере()

// Процедура - обработчик события ПередЗаписьюНаСервере.
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СтруктураНастроекОтборов = Новый Структура;
	СтруктураНастроекОтборов.Вставить("СписокНоменклатуры", СписокНоменклатуры);
	СтруктураНастроекОтборов.Вставить("СписокГруппНоменклатуры", СписокГруппНоменклатуры);
	СтруктураНастроекОтборов.Вставить("СписокКатегорийНоменклатуры", СписокКатегорийНоменклатуры);
	
	ТекущийОбъект.НастройкиОтборов = Новый ХранилищеЗначения(СтруктураНастроекОтборов);
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	СсылкаУсловие = ТекущийОбъект.Ссылка;
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Условие");
		Если Не Параметр = Неопределено Тогда
			Если Не ЗначениеЗаполнено(СсылкаУсловие) Тогда
				СсылкаУсловие = Документы.ИнвентаризацияЗапасов.ПолучитьСсылку(Новый УникальныйИдентификатор());
				ТекущийОбъект.УстановитьСсылкуНового(СсылкаУсловие);
			КонецЕсли;		
			Параметр.Значение = СсылкаУсловие;
		КонецЕсли;
		
		// Данные объекты используются только для возможности редактировать настройки в интерфейсе
		Настройки = НастройкаКомпановкиДанных.ПолучитьНастройки();
		Если НЕ ЗначениеЗаполнено(АдресСхемыКомпановкиДанных) Тогда
			СхемаКомпановкиДанныхУсловия = Документы.ИнвентаризацияЗапасов.ПолучитьМакет(?(ЗначениеЗаполнено(Объект.ИмяСхемыКомпоновкиДанных),Объект.ИмяСхемыКомпоновкиДанных,"ТиповаяСхема")); 
		Иначе
		СхемаКомпановкиДанныхУсловия = ПолучитьИзВременногоХранилища(АдресСхемыКомпановкиДанных); 
		КонецЕсли;
		Параметр = СхемаКомпановкиДанныхУсловия.Параметры.Найти("Условие");
		Если Не Параметр = Неопределено Тогда
			Если Не ЗначениеЗаполнено(СсылкаУсловие) Тогда
				СсылкаУсловие = Документы.ИнвентаризацияЗапасов.ПолучитьСсылку(Новый УникальныйИдентификатор());
				ТекущийОбъект.УстановитьСсылкуНового(СсылкаУсловие);
			КонецЕсли;		
			Параметр.Значение = СсылкаУсловие;
		КонецЕсли;
		
		ТекущийОбъект.ХранилищеСхемыКомпоновкиДанных = Новый ХранилищеЗначения(СхемаКомпановкиДанныхУсловия);
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(Настройки);
		
		// Фактически, для выполнения будет использоваться уже заранее скомпанованный макет.
		// Для каждого отдельного документа будут обновляться значения параметров, и данные в менеджере временных таблиц
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпановкиДанныхУсловия, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		ТекущийОбъект.ХранилищеСкомпанованногоМакета = Новый ХранилищеЗначения(МакетКомпоновки);
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	//Обсуждения
	ОбсужденияУНФ.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// ИнтеграцияГосИС
	СобытияФормИСУНФ.ПослеЗаписиНаСервере(ЭтаФорма);
	// Конец ИнтеграцияГосИС
	
	
		//Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Организация");
		//Если Не Параметр = Неопределено Тогда
		//	Параметр.Значение = Объект.Организация;
		//КонецЕсли;
		//Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("СтруктурнаяЕдиница");
		//Если Не Параметр = Неопределено Тогда
		//	Параметр.Значение = Объект.СтруктурнаяЕдиница;
		//КонецЕсли;
		//Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Период");
		//Если Не Параметр = Неопределено Тогда
		//	Параметр.Значение = Объект.Дата;
		//КонецЕсли;
		
		// Данные объекты используются только для возможности редактировать настройки в интерфейсе
		Настройки = НастройкаКомпановкиДанных.ПолучитьНастройки();
		Если НЕ ЗначениеЗаполнено(АдресСхемыКомпановкиДанных) Тогда
			СхемаКомпановкиДанныхУсловия = Документы.ИнвентаризацияЗапасов.ПолучитьМакет(?(ЗначениеЗаполнено(Объект.ИмяСхемыКомпоновкиДанных),Объект.ИмяСхемыКомпоновкиДанных,"ТиповаяСхема")); 
		Иначе
		СхемаКомпановкиДанныхУсловия = ПолучитьИзВременногоХранилища(АдресСхемыКомпановкиДанных); 
		КонецЕсли;
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Организация");
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение = Объект.Организация;
		КонецЕсли;
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("СтруктурнаяЕдиница");
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение = Объект.СтруктурнаяЕдиница;
		КонецЕсли;
		Параметр = НастройкаКомпановкиДанных.Настройки.ПараметрыДанных.Элементы.Найти("Период");
		Если Не Параметр = Неопределено Тогда
			Параметр.Значение = Объект.Дата;
		КонецЕсли;
		
		ТекущийОбъект.ХранилищеСхемыКомпоновкиДанных = Новый ХранилищеЗначения(СхемаКомпановкиДанныхУсловия);
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(Настройки);
		
		// Фактически, для выполнения будет использоваться уже заранее скомпанованный макет.
		// Для каждого отдельного документа будут обновляться значения параметров, и данные в менеджере временных таблиц
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпановкиДанныхУсловия, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		ТекущийОбъект.ХранилищеСкомпанованногоМакета = Новый ХранилищеЗначения(МакетКомпоновки);
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	
	
	УправлениеФормой();
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события ПриЗакрытии.
//
&НаКлиенте
Процедура ПриЗакрытии()
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры // ПриЗакрытии()

// Процедура - обработчик события ОбработкаОповещения формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Данные = МенеджерОборудованияУНФКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			ПолученыШтрихкоды(Данные);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если ИмяСобытия = "ПодборПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
		
		АдресЗапасовВХранилище = Параметр;
		
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, "Запасы", Истина, Истина);
		
	ИначеЕсли ИмяСобытия = "ПодборСерий"
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
		
		ИзменилосьКоличество = ПолучитьСерииНоменклатурыИзХранилища(Параметр.АдресВоВременномХранилище, Параметр.КлючСтроки);
		Если ИзменилосьКоличество Тогда
			РассчитатьСуммуВСтрокеТабличнойЧасти();
		КонецЕсли; 
		
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Запасы");
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	// ИнтеграцияГосИС
	СобытияФормИСУНФКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтеграцияГосИС

КонецПроцедуры // ОбработкаОповещения()

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	// ИнтеграцияГосИС
	СобытияФормИСУНФКлиент.ОбработкаНавигационнойСсылки(ЭтаФорма, НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	// Конец ИнтеграцияГосИС
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДанныеДляИзмененияДаты = ДокументыУНФКлиент.ДанныеДляИзмененияДаты(ЭтотОбъект, Объект);
	Если ДанныеДляИзмененияДаты.ДатаНеМенялась Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПриИзмененииНаСервере(ДанныеДляИзмененияДаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыПечатиНажатие(Элемент)
	
	ОткрытьФормуРеквизитыПечати();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СтруктурнаяЕдиница.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	
	ОбновитьРеквизитыВидимостиФормы();
	УправлениеФормой();
	
	ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита();
	ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов();
	 СхемаКомпоновкиДанныхПриИзмененииНаСервере(Ложь);
КонецПроцедуры // СтруктурнаяЕдиницаПриИзменении()

// Процедура - обработчик события Открытие поля СтруктурнаяЕдиница.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиница.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаОткрытие()

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапасы

// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
&НаКлиенте
Процедура ЗапасыНоменклатураПриИзменении(Элемент)

	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗапрашиватьСбросКоличества И СбрасыватьКоличество
		И СтрокаТабличнойЧасти.Количество <> НовоеКоличествоПриИзмененииНоменклатуры Тогда
		
		ДопПараметр = Новый Структура();
		ДопПараметр.Вставить("Элемент", Элемент);
		ДопПараметр.Вставить("Количество", СтрокаТабличнойЧасти.Количество);
		Оповещение = Новый ОписаниеОповещения("ЗапасыПодтверждениеСбросаКоличестваЗавершение", ЭтотОбъект, ДопПараметр);
		ОткрытьФорму("ОбщаяФорма.ПодтверждениеСбросаКоличества", , , , , , Оповещение);
		
	Иначе
		
		Если СбрасыватьКоличество Тогда
			НовоеКоличество = НовоеКоличествоПриИзмененииНоменклатуры;
		Иначе
			НовоеКоличество = СтрокаТабличнойЧасти.Количество;
		КонецЕсли;
		ЗапасыНоменклатураПриИзмененииПродолжить(Элемент, НовоеКоличество);
		
	КонецЕсли;
	
КонецПроцедуры // ЗапасыНоменклатураПриИзменении()

&НаКлиенте
Процедура ЗапасыПодтверждениеСбросаКоличестваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ЗапрашиватьСбросКоличества = Результат.ЗапрашиватьСбросКоличества;
		Если НЕ ЗапрашиватьСбросКоличества Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		КонецЕсли;
		Если Результат.СбрасыватьКоличество Тогда
			НовоеКоличество = Результат.НовоеКоличествоПриИзмененииНоменклатуры;
		Иначе
			НовоеКоличество = ДополнительныеПараметры.Количество;
		КонецЕсли;
	Иначе
		НовоеКоличество = ДополнительныеПараметры.Количество;
	КонецЕсли;
	
	ЗапасыНоменклатураПриИзмененииПродолжить(ДополнительныеПараметры.Элемент, НовоеКоличество);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНоменклатураПриИзмененииПродолжить(Элемент, НовоеКоличество)

	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ОтветственноеХранение"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ТоварыНаКомиссии"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	СерииНоменклатурыУНФКлиентСервер.ДополнитьСтруктуруДаннымиДляПолученияСерийНоменклатуры(Объект, СтруктураДанные);
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество = НовоеКоличество;	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	СтрокаТабличнойЧасти.КоличествоУчет = 0;
	СтрокаТабличнойЧасти.СуммаУчет = 0;
	
	// Расчет отклонения.
	СтрокаТабличнойЧасти.Отклонение = СтрокаТабличнойЧасти.Количество - СтрокаТабличнойЧасти.КоличествоУчет;
	СтрокаТабличнойЧасти.Излишки = ?(СтрокаТабличнойЧасти.Отклонение < 0, 0,СтрокаТабличнойЧасти.Отклонение);
	СтрокаТабличнойЧасти.Недостача = ?(СтрокаТабличнойЧасти.Отклонение > 0, 0,-СтрокаТабличнойЧасти.Отклонение); 
	СтрокаТабличнойЧасти.ИзлишкиСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение < 0, Ложь,Истина); 
	СтрокаТабличнойЧасти.НедостачаСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение > 0, Ложь,Истина);  
	СтрокаТабличнойЧасти.ОтклонениеСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение = 0, Ложь,Истина);

	// Серии номенклатуры
	Для каждого ВыделеннаяСтрока Из Элементы.Запасы.ВыделенныеСтроки Цикл
		ТекущиеДанныеСтроки = Элементы.Запасы.ДанныеСтроки(ВыделеннаяСтрока);
		СерииНоменклатурыУНФКлиентСервер.УдалитьСерииНоменклатурыПоКлючуСвязи(Объект.СерииНоменклатуры, ТекущиеДанныеСтроки,
			ИспользоватьСерииНоменклатурыОстатки);
	КонецЦикла;
	
	СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
			СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
	КонецЕсли;
	
	//Партии
	СтрокаТабличнойЧасти.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Партия = СтруктураВыбораНоменклатуры.Партия;
			СтруктураВыбораНоменклатуры.Партия = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Партия = СтруктураДанные.Партия;
		КонецЕсли;
	КонецЕсли;
	// Конец Партии
	
	СтрокаТабличнойЧасти.СтатусыСерийНоменклатуры = СтруктураДанные.СтатусыСерийНоменклатуры;
	
	ПодборНоменклатурыИзСписка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
		
		Если ВыбранноеЗначение.Свойство("СтруктураНастроек") Тогда
			НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "ИнвентаризацияЗапасов", НастройкиФормыВыбораНоменклатуры, Истина, "Запасы", ВыбранноеЗначение.СтруктураНастроек);
			НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
		
		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаВыбора поля ввода ЕдиницаИзмерения.
//
&НаКлиенте
Процедура ЗапасыЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыбранноеЗначение 
		ИЛИ СтрокаТабличнойЧасти.Цена = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКоэффициент = 0;
	Если ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		ТекущийКоэффициент = 1;
	КонецЕсли;
	
	Коэффициент = 0;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	Если ТекущийКоэффициент = 0 И Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения, ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ИначеЕсли Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(,ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 1 И Коэффициент = 1 Тогда
		СтруктураДанные = Новый Структура("ТекущийКоэффициент, Коэффициент", 1, 1);
	КонецЕсли;
	
	Если СтруктураДанные.ТекущийКоэффициент <> 0 И СтруктураДанные.Коэффициент <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Цена * СтруктураДанные.Коэффициент / СтруктураДанные.ТекущийКоэффициент;
		СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество * СтруктураДанные.ТекущийКоэффициент / СтруктураДанные.Коэффициент;
		СтрокаТабличнойЧасти.КоличествоУчет = СтрокаТабличнойЧасти.КоличествоУчет * СтруктураДанные.ТекущийКоэффициент / СтруктураДанные.Коэффициент;
	КонецЕсли;
	
	// Расчет отклонения.
	СтрокаТабличнойЧасти.Отклонение = СтрокаТабличнойЧасти.Количество - СтрокаТабличнойЧасти.КоличествоУчет;
	
КонецПроцедуры // ЗапасыЕдиницаИзмеренияОбработкаВыбора()

// Процедура - обработчик события ПриИзменении поля ввода Количество
// в строке табличной части Запасы.
// Осуществляет пересчет суммы в строке табличной части.
//
&НаКлиенте
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	СтрокаТабличнойЧасти = Элементы["Запасы"].ТекущиеДанные; 
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, СтрокаТабличнойЧасти);
	
	МассивИменКолонокДляПодсветки = Новый Массив;
	МассивИменКолонокДляПодсветки.Добавить(Элементы.Запасы.ПодчиненныеЭлементы.ЗапасыНоменклатура.Имя);
	МассивИменКолонокДляПодсветки.Добавить(Элементы.Запасы.ПодчиненныеЭлементы.ЗапасыКоличество.Имя);
	
КонецПроцедуры // ТоварыКоличествоЕдиницПриИзменении()


&НаКлиенте
Процедура ЗапасыЕдиницаИзмеренияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы["Запасы"].ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат 
	КонецЕсли;
	СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТабличнойЧасти = Неопределено)
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	КонецЕсли;
	
	// Расчет отклонения.
	СтрокаТабличнойЧасти.Отклонение = СтрокаТабличнойЧасти.Количество - СтрокаТабличнойЧасти.КоличествоУчет; 
	СтрокаТабличнойЧасти.Излишки = ?(СтрокаТабличнойЧасти.Отклонение < 0, 0,СтрокаТабличнойЧасти.Отклонение);
	СтрокаТабличнойЧасти.Недостача = ?(СтрокаТабличнойЧасти.Отклонение > 0, 0,-СтрокаТабличнойЧасти.Отклонение);
	СтрокаТабличнойЧасти.ИзлишкиСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение < 0, Ложь,Истина); 
	СтрокаТабличнойЧасти.НедостачаСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение > 0, Ложь,Истина); 
	СтрокаТабличнойЧасти.ОтклонениеСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение = 0, Ложь,Истина);

	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена; 
	
	
	// Серии номенклатуры
	Если ИспользоватьСерииНоменклатурыОстатки<>Неопределено Тогда
		СерииНоменклатурыУНФКлиентСервер.ОбновитьСерииНоменклатурыКоличество(Объект, СтрокаТабличнойЧасти);
	КонецЕсли;	
	
КонецПроцедуры // РассчитатьСуммуВСтрокеТабличнойЧасти()

// Процедура - обработчик события ПриИзменении поля ввода Цена
// в строке табличной части Запасы.
// Осуществляет пересчет сумм в строке табличной части.
//
&НаКлиенте
Процедура ЗапасыЦенаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Сумма
// в строке табличной части Запасы.
// Осуществляет пересчет цен в строке табличной части.
//
&НаКлиенте
Процедура ЗапасыСуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	ЦенаСтроки = ?(СтрокаТабличнойЧасти.Количество = 0, 0, СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество);
	
	СтрокаТабличнойЧасти.Цена = ?(ЦенаСтроки < 0, -1 * ЦенаСтроки, ЦенаСтроки);
	
КонецПроцедуры // ЗапасыСуммаПриИзменении()

&НаКлиенте
Процедура ЗапасыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Копирование Тогда
		Элемент.ТекущиеДанные.КлючСвязи = 0;
		Элемент.ТекущиеДанные.СерииНоменклатуры = "";
		СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, Элемент.ТекущиеДанные);
	КонецЕсли;	

	Если Элемент.ТекущийЭлемент = Элементы.ЗапасыСерииНоменклатуры Тогда
		ОткрытьПодборСерииНоменклатуры();
	КонецЕсли;
	
	СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
	Если НоваяСтрока И НЕ Копирование Тогда
		СтрокаТабличнойЧасти.Количество = НовоеКоличествоПриИзмененииНоменклатуры;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПередУдалением(Элемент, Отказ)
	
	// Серии номенклатуры
	Для каждого ВыделеннаяСтрока Из Элементы.Запасы.ВыделенныеСтроки Цикл
		ТекущиеДанныеСтроки = Элементы.Запасы.ДанныеСтроки(ВыделеннаяСтрока);
		СерииНоменклатурыУНФКлиентСервер.УдалитьСерииНоменклатурыПоКлючуСвязи(Объект.СерииНоменклатуры, ТекущиеДанныеСтроки,
			ИспользоватьСерииНоменклатурыОстатки);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура ЗаполнитьОстатки(Команда)
	Объект.ИмяСхемыКомпоновкиДанных = "ТиповаяСхема" ;    
	СхемаКомпоновкиДанныхПриИзмененииНаСервере(Ложь);
	АдресСхемыДляРедактирования = ПоместитьКопиюСхемыВоВременноеХранилище(АдресСхемыКомпановкиДанных, УникальныйИдентификатор);
	АдресХранилищаНастройкиКомпоновщика = ПоместитьНастройкиСКДВоВременноеХранилище();
	ЗаполнитьОстаткиНаСервере();


КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиНаСервере()
	НовыеНастройки = НастройкаКомпановкиДанных.ПолучитьНастройки();
	ТиповаяСхема = Документы.ИнвентаризацияЗапасов.ПолучитьМакет(Объект.ИмяСхемыКомпоновкиДанных);
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(ТиповаяСхема, НовыеНастройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

    ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКД.Инициализировать(МакетКомпоновки);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ТаблицаЗначений = Новый ТаблицаЗначений;
    ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
    ПроцессорВывода.Вывести(ПроцессорКД);
	Объект.Запасы.Очистить();
	Объект.Запасы.Загрузить(ТаблицаЗначений);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
КонецПроцедуры



&НаКлиенте
Процедура КомандаЗаполнитьТолькоУчетныеДанныеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Ответ = Результат;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	КомандаЗаполнитьТолькоУчетныеДанныеФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьТолькоУчетныеДанныеФрагмент()

	ЗаполнитьТолькоУчетныеДанные();

КонецПроцедуры // ЗаполнитьТолькоУчетныеДанные()

&НаКлиенте
Процедура КомандаЗаполнитьПоВидуЦенЗавершение1(Результат, ДополнительныеПараметры) Экспорт

	Ответ = Результат;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ОткрытьФорму("Справочник.ВидыЦен.Форма.ФормаВыбора", , , , , ,
		Новый ОписаниеОповещения("КомандаЗаполнитьПоВидуЦенЗавершение", ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоВидуЦенЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ВидЦен = Результат;

	Если ТипЗнч(ВидЦен) = Тип("СправочникСсылка.ВидыЦен") Тогда
		ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен(ВидЦен);
	КонецЕсли;

КонецПроцедуры // КомандаЗаполнитьПоВидуЦен()

&НаКлиенте
Процедура КомандаОбнулитьКоличествоИСуммуЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Ответ = Результат;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	КомандаОбнулитьКоличествоИСуммуФрагмент();
	Если ИзлишкиОтбор Тогда
		Элементы.Запасы.ОтборСтрок 	= Неопределено; 
		ИзлишкиОтбор 				= Ложь;
		Элементы.ГруппаИзлишки.ЦветФона = Новый Цвет(); 
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбнулитьКоличествоИСуммуФрагмент()

	Перем СтрокаТабличнойЧасти;

	Для Каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
    Если СтрокаТабличнойЧасти.Пометка Тогда
		СтрокаТабличнойЧасти.Количество = 0;
		СтрокаТабличнойЧасти.Сумма = 0;
		СтрокаТабличнойЧасти.Отклонение = СтрокаТабличнойЧасти.Количество - СтрокаТабличнойЧасти.КоличествоУчет;
		СтрокаТабличнойЧасти.Излишки = ?(СтрокаТабличнойЧасти.Отклонение < 0, 0,СтрокаТабличнойЧасти.Отклонение);
		СтрокаТабличнойЧасти.Недостача = ?(СтрокаТабличнойЧасти.Отклонение > 0, 0,-СтрокаТабличнойЧасти.Отклонение);
		СтрокаТабличнойЧасти.ИзлишкиСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение < 0, Ложь,Истина); 
		СтрокаТабличнойЧасти.НедостачаСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение > 0, Ложь,Истина);  
		СтрокаТабличнойЧасти.ОтклонениеСоответствуетОтбору = ?(СтрокаТабличнойЧасти.Отклонение = 0, Ложь,Истина); 
	КонецЕсли;
	КонецЦикла;

	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоГруппам(Команда)
	СортироватьТаблицуТоваров();
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоКатегориям(Команда)
	СортироватьТаблицуТоваров(Истина);
КонецПроцедуры

&НаСервере
Процедура СортироватьТаблицуТоваров(ПоКатегориям = Ложь)
	
	ТаблицаТовары = Объект.Запасы.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Партия КАК Партия,
	|	Товары.СерииНоменклатуры КАК СерииНоменклатуры,
	|	Товары.Отклонение КАК Отклонение,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУчет КАК КоличествоУчет,
	|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Товары.Цена КАК Цена,
	|	Товары.Сумма КАК Сумма,
	|	Товары.СуммаУчет КАК СуммаУчет,
	|	Товары.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ Вт_Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Товары.Номенклатура КАК Номенклатура,
	|	Вт_Товары.Характеристика КАК Характеристика,
	|	Вт_Товары.Партия КАК Партия,
	|	Вт_Товары.СерииНоменклатуры КАК СерииНоменклатуры,
	|	Вт_Товары.Отклонение КАК Отклонение,
	|	Вт_Товары.Количество КАК Количество,
	|	Вт_Товары.КоличествоУчет КАК КоличествоУчет,
	|	Вт_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Вт_Товары.Цена КАК Цена,
	|	Вт_Товары.Сумма КАК Сумма,
	|	Вт_Товары.СуммаУчет КАК СуммаУчет,
	|	Вт_Товары.КлючСвязи КАК КлючСвязи,
	|	СпрНоменклатура.КатегорияНоменклатуры.Наименование КАК КатегорияНоменклатурыНаименование,
	|	СпрНоменклатура.Родитель.Наименование КАК РодительНаименование
	|ИЗ
	|	Вт_Товары КАК Вт_Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО Вт_Товары.Номенклатура = СпрНоменклатура.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	&ПолеСортировки,
	|	СпрНоменклатура.Наименование";
	
	Если ПоКатегориям Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеСортировки", "КатегорияНоменклатурыНаименование");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеСортировки", "РодительНаименование");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Объект.Запасы.Очистить();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипЗаписи() <> ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
	КонецЦикла;
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСбросаКоличества(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СтрокаПоиска", НСтр("ru = 'Сбрасывать количество при изменении номенклатуры'"));
	Оповещение = Новый ОписаниеОповещения("НастройкаСбросаКоличестваЗавершение", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.НастройкиПользователей.Форма.ФормаНастройкиПользователя", СтруктураПараметров, , , , ,
		Оповещение);
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, 
		Элементы.ЗапасыКонтекстноеМенюНастройкаСбросаКоличества, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСбросаКоличестваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	НастройкиКоличества = ПолучитьНастройкиИзмененияКоличества();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиКоличества);
	
КонецПроцедуры

&НаСервере
Процедура РедактироватьСхемуКомпоновкиДанныхЗавершениеНаСервере(Результат, ДополнительныеПараметры) Экспорт

 	АдресХранилищаНастройкиКомпоновщика = Результат;

	Если ЗначениеЗаполнено(АдресХранилищаНастройкиКомпоновщика) Тогда
		// Схема могла быть модифицирована
		АдресИзмененнойСхемы = Неопределено;
		Если ДополнительныеПараметры.Свойство("АдресСхемыДляРедактирования", АдресИзмененнойСхемы) Тогда
			ТиповаяСхема = Документы.ИнвентаризацияЗапасов.ПолучитьМакет(Объект.ИмяСхемыКомпоновкиДанных);
			НоваяСхема = ПолучитьИзВременногоХранилища(АдресИзмененнойСхемы);
			Если ЦенообразованиеСервер.ПолучитьXML(НоваяСхема) = ЦенообразованиеСервер.ПолучитьXML(ТиповаяСхема) Тогда
				Объект.СхемаСКДМодифицированаВручную = Ложь;
			Иначе
				Объект.СхемаСКДМодифицированаВручную = Истина;
				ИмяСхемыСКДНаФорме = "Произвольная";
				ПоместитьВоВременноеХранилище(НоваяСхема, АдресСхемыКомпановкиДанных);
				ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпановкиДанных);
				НастройкаКомпановкиДанных.Инициализировать(ИсточникНастроек);
			КонецЕсли;
		КонецЕсли;

		НовыеНастройки = ПолучитьИзВременногоХранилища(АдресХранилищаНастройкиКомпоновщика);		
		НастройкаКомпановкиДанных.ЗагрузитьНастройки(НовыеНастройки);
		НастройкаКомпановкиДанных.Восстановить();
		Модифицированность = Истина;
	
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(?(Объект.СхемаСКДМодифицированаВручную,НоваяСхема,ТиповаяСхема), НовыеНастройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

    ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКД.Инициализировать(МакетКомпоновки);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ТаблицаЗначений = Новый ТаблицаЗначений;
    ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
    ПроцессорВывода.Вывести(ПроцессорКД);
	Объект.Запасы.Очистить();
	Объект.Запасы.Загрузить(ТаблицаЗначений);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьТиповСтруктурныхЕдиниц()
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(Перечисления.ТипыСтруктурныхЕдиниц.Склад);
	НовыйМассив.Добавить(Перечисления.ТипыСтруктурныхЕдиниц.Розница);
	Если Константы.ФункциональнаяОпцияИспользоватьПодсистемуПроизводство.Получить() Тогда
		НовыйМассив.Добавить(Перечисления.ТипыСтруктурныхЕдиниц.Подразделение);
	КонецЕсли;
	МассивСкладПодразделениеРозница = Новый ФиксированныйМассив(НовыйМассив);
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипСтруктурнойЕдиницы", МассивСкладПодразделениеРозница);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.СтруктурнаяЕдиница.ПараметрыВыбора = НовыеПараметры;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов()
	ДополнительныеСвойства = Новый Структура("ИзменениеКлючевыхРеквизитов", Истина);
	СерииНоменклатурыУНФ.ЗаполнитьПризнакиИспользованияСерий(Объект, ДополнительныеСвойства);
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита()
	НоменклатураВДокументахСервер.ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита(ЭтотОбъект,
		"ИнвентаризацияЗапасов", "Запасы", НастройкиФормыВыбораНоменклатуры);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияРеквизитовПечати(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ПечатьДокументовУНФКлиент.ОбновитьЗначенияРеквизитовПечати(ЭтотОбъект, Результат.ИзмененныеРеквизиты);
		
		Если Результат.Свойство("Команда") Тогда
			
			Подключаемый_ВыполнитьКоманду(Результат.Команда);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРеквизитыВидимостиФормы()
	
	СтрокаРеквизитов = "ТипСтруктурнойЕдиницы, ОрдерныйСклад";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СтруктурнаяЕдиница, СтрокаРеквизитов);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ЗначенияРеквизитов, СтрокаРеквизитов);
	
	УчетПоЯчейкам = ПолучитьФункциональнуюОпцию("УчетПоЯчейкам");
	
КонецПроцедуры

// Функция возвращает текст запроса по учетным данным на складе.
//
&НаСервере
Функция СформироватьТекстЗапросаУчетныеДанныеНаСкладе()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК ПризнакВЯчейке,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК Количество,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоУчет,
	|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаУчет
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасыОстатки
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(
	|			&Период,
	|			Организация = &Организация
	|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И (Номенклатура, Характеристика, Партия) В
	|					(ВЫБРАТЬ
	|						ИнвентаризацияЗапасов.Номенклатура КАК Номенклатура,
	|						ИнвентаризацияЗапасов.Характеристика КАК Характеристика,
	|						ИнвентаризацияЗапасов.Партия КАК Партия
	|					ИЗ
	|						ИнвентаризацияЗапасов КАК ИнвентаризацияЗапасов)) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Партия";
	
	Возврат ТекстЗапроса + Символы.ПС +
		";
		|
		|////////////////////////////////////////////////////////////////////////////////"
		+ Символы.ПС;
	
КонецФункции // СформироватьТекстЗапросаУчетныеДанныеНаСкладе()

// Функция возвращает текст запроса по учетным данным в ячейке на складе.
//
&НаСервере
Функция СформироватьТекстЗапросаУчетныеДанныеВЯчейкеНаСкладе()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИСТИНА КАК ПризнакВЯчейке,
	|	ЗапасыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗапасыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ЗапасыНаСкладахОстатки.Партия КАК Партия,
	|	СУММА(ЗапасыНаСкладахОстатки.КоличествоОстаток) КАК Количество,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоУчет,
	|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаУчет
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасыОстатки
	|ИЗ
	|	РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|			&Период,
	|			Организация = &Организация
	|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И Ячейка = &Ячейка
	|				И (Номенклатура, Характеристика, Партия) В
	|					(ВЫБРАТЬ
	|						ИнвентаризацияЗапасов.Номенклатура КАК Номенклатура,
	|						ИнвентаризацияЗапасов.Характеристика КАК Характеристика,
	|						ИнвентаризацияЗапасов.Партия КАК Партия
	|					ИЗ
	|						ИнвентаризацияЗапасов КАК ИнвентаризацияЗапасов)) КАК ЗапасыНаСкладахОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
	|				&Период,
	|				Организация = &Организация
	|					И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ЗапасыОстатки
	|		ПО ЗапасыНаСкладахОстатки.Номенклатура = ЗапасыОстатки.Номенклатура
	|			И ЗапасыНаСкладахОстатки.Характеристика = ЗапасыОстатки.Характеристика
	|			И ЗапасыНаСкладахОстатки.Партия = ЗапасыОстатки.Партия
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыНаСкладахОстатки.Номенклатура,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения,
	|	ЗапасыНаСкладахОстатки.Характеристика,
	|	ЗапасыНаСкладахОстатки.Партия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Партия";
	
	Возврат ТекстЗапроса + Символы.ПС +
		";
		|
		|////////////////////////////////////////////////////////////////////////////////"
		+ Символы.ПС;
	
КонецФункции // СформироватьТекстЗапросаУчетныеДанныеВЯчейкеНаСкладе()

// Процедура заполняет табличную часть "Запасы" учетными данными.
// 
&НаСервере
Процедура ЗаполнитьТолькоУчетныеДанные()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗатраты.Номенклатура КАК Номенклатура,
	|	ТаблицаЗатраты.Характеристика КАК Характеристика,
	|	ТаблицаЗатраты.Партия КАК Партия,
	|	ТаблицаЗатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаЗатраты.Количество КАК Количество,
	|	ТаблицаЗатраты.Цена КАК Цена,
	|	ТаблицаЗатраты.Сумма КАК Сумма,
	|	ТаблицаЗатраты.Пометка КАК Пометка,
	|	ТаблицаЗатраты.КоличествоУчет КАК КоличествоУчет,
	|	ТаблицаЗатраты.СуммаУчет КАК СуммаУчет,
	|	ТаблицаЗатраты.Отклонение КАК Отклонение
	|ПОМЕСТИТЬ ИнвентаризацияЗапасов
	|ИЗ
	|	&ТаблицаЗатраты КАК ТаблицаЗатраты";
	
	Запрос.УстановитьПараметр("ТаблицаЗатраты", Объект.Запасы.Выгрузить());
	
	Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ЗначениеЗаполнено(Объект.Ячейка) Тогда
		ТекстЗапроса = СформироватьТекстЗапросаУчетныеДанныеВЯчейкеНаСкладе();
		Запрос.УстановитьПараметр("Ячейка", Объект.Ячейка);
	Иначе
		ТекстЗапроса = СформироватьТекстЗапросаУчетныеДанныеНаСкладе();
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ИнвентаризацияЗапасовЗапасы.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияЗапасовЗапасы.Характеристика КАК Характеристика,
	|	ИнвентаризацияЗапасовЗапасы.Партия КАК Партия,
	|	ИнвентаризацияЗапасовЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИнвентаризацияЗапасовЗапасы.Количество КАК Количество,
	|	ИнвентаризацияЗапасовЗапасы.Цена КАК Цена,
	|	ИнвентаризацияЗапасовЗапасы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияЗапасовЗапасы.Пометка
	|			ТОГДА ЕСТЬNULL(ВТЗапасыОстатки.Количество, 0) / ЕСТЬNULL(ИнвентаризацияЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ИнвентаризацияЗапасовЗапасы.Количество
	|	КОНЕЦ КАК КоличествоВЯчейке,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияЗапасовЗапасы.Пометка
	|			ТОГДА ЕСТЬNULL(ВТЗапасыОстатки.КоличествоУчет, 0) / ЕСТЬNULL(ИнвентаризацияЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ИнвентаризацияЗапасовЗапасы.КоличествоУчет
	|	КОНЕЦ КАК КоличествоУчет,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияЗапасовЗапасы.Пометка
	|			ТОГДА ЕСТЬNULL(ВТЗапасыОстатки.СуммаУчет, 0)
	|		ИНАЧЕ ИнвентаризацияЗапасовЗапасы.СуммаУчет
	|	КОНЕЦ КАК СуммаУчет,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияЗапасовЗапасы.Пометка
	|			ТОГДА ВЫБОР
	|					КОГДА ВТЗапасыОстатки.ПризнакВЯчейке
	|						ТОГДА ИнвентаризацияЗапасовЗапасы.Количество - ЕСТЬNULL(ВТЗапасыОстатки.Количество, 0) / ЕСТЬNULL(ИнвентаризацияЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
	|					ИНАЧЕ ИнвентаризацияЗапасовЗапасы.Количество - ЕСТЬNULL(ВТЗапасыОстатки.КоличествоУчет, 0) / ЕСТЬNULL(ИнвентаризацияЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
	|				КОНЕЦ
	|		ИНАЧЕ ИнвентаризацияЗапасовЗапасы.Отклонение
	|	КОНЕЦ КАК Отклонение,
	|	ИнвентаризацияЗапасовЗапасы.Пометка КАК Пометка
	|ИЗ
	|	ИнвентаризацияЗапасов КАК ИнвентаризацияЗапасовЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаЗапасыОстатки КАК ВТЗапасыОстатки
	|		ПО ИнвентаризацияЗапасовЗапасы.Номенклатура = ВТЗапасыОстатки.Номенклатура
	|			И ИнвентаризацияЗапасовЗапасы.Характеристика = ВТЗапасыОстатки.Характеристика
	|			И ИнвентаризацияЗапасовЗапасы.Партия = ВТЗапасыОстатки.Партия";
	
	Запрос.УстановитьПараметр("Организация", Компания);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Период", КонецДня(Объект.Дата));
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если ЗначениеЗаполнено(Объект.Ячейка) Тогда
		
		Объект.Запасы.Очистить();
		Объект.СерииНоменклатуры.Очистить();
		
		Выборка = МассивРезультатов[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Объект.Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			Если Выборка.КоличествоВЯчейке <> Выборка.КоличествоУчет Тогда
				
				НоваяСтрока.КоличествоУчет = Выборка.КоличествоВЯчейке;
				Если Выборка.КоличествоВЯчейке = 0
					ИЛИ Выборка.КоличествоУчет <= 0
					ИЛИ Выборка.СуммаУчет = 0 Тогда
					НоваяСтрока.СуммаУчет = 0;
				Иначе
					НоваяСтрока.СуммаУчет = Выборка.СуммаУчет / Выборка.КоличествоУчет * НоваяСтрока.КоличествоУчет;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Объект.Запасы.Загрузить(МассивРезультатов[1].Выгрузить());
		
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
КонецПроцедуры // ЗаполнитьТолькоУчетныеДанные()

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ДанныеДляИзмененияДаты)
	
	ДокументыУНФ.ДатаПриИзменении(ДанныеДляИзмененияДаты, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ДокументыУНФ.ОрганизацияПриИзменении(ЭтотОбъект, Объект);
	Объект.ПодписьПроверяющего = Объект.Организация.ПодписьГлавногоБухгалтера;
	
	ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита();
	ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	// Наборы
	СтруктураДанные.Вставить("ЭтоНабор", СтруктураДанные.Номенклатура.ЭтоНабор);
	
	// Характеристики
	СтруктураДанные.Вставить("ИспользоватьХарактеристики",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",Ложь);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьХарактеристики
		Тогда
		ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		Если Не ЗначенияПоУмолчанию = Неопределено
			Тогда		
			ХарактеристикаПоУмолчанию = ЗначенияПоУмолчанию;
		КонецЕсли;
		
		Если НЕ СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Характеристика",ХарактеристикаПоУмолчанию);
		Иначе
			СтруктураДанные.Характеристика = ?(ЗначениеЗаполнено(СтруктураДанные.Характеристика), СтруктураДанные.Характеристика, ХарактеристикаПоУмолчанию);
		КонецЕсли;
		
		СтруктураДанные.Вставить("ИспользоватьХарактеристики",Истина);
		СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",СтруктураДанные.Номенклатура.ПроверятьЗаполнениеХарактеристики);
	КонецЕсли; 
	// Конец Характеристики
	
	//Партии
	СтруктураДанные.Вставить("ИспользоватьПартии",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеПартий",Ложь);
	
	Если НЕ СтруктураДанные.Свойство("Партия") Тогда
		СтруктураДанные.Вставить("Партия", Неопределено);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьПартии
		Тогда
		
		Если СтруктураДанные.Свойство("СтатусПартии")
			Тогда
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтруктураДанные.СтатусПартии);
		Иначе
			Если Не СтруктураДанные.Свойство("ВидОперации")
				Тогда
				ВидОперации = Неопределено
			Иначе
				ВидОперации = СтруктураДанные.ВидОперации
			КонецЕсли;
			
			СтатусПартии = НоменклатураВДокументахСервер.СоответствиеВидаОперацииИлиХозОперацииСтатусуПартии(, ВидОперации);
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтатусПартии);
		КонецЕсли;
		
		ПартияПоУмолчанию = Справочники.ПартииНоменклатуры.ПустаяСсылка();
		
		Если Не ЗначенияПартииПоУмолчанию = Неопределено
			Тогда
			ПартияПоУмолчанию = ЗначенияПартииПоУмолчанию;
		КонецЕсли;
		
		СтруктураДанные.Партия = ?(ЗначениеЗаполнено(СтруктураДанные.Партия), СтруктураДанные.Партия, ПартияПоУмолчанию);
		
		СтруктураДанные.ПроверятьЗаполнениеПартий = СтруктураДанные.Номенклатура.ПроверятьЗаполнениеПартий;
		СтруктураДанные.ИспользоватьПартии = Истина;
		
	КонецЕсли;
	// Конец Партии
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьСерииНоменклатуры Тогда
		СтруктураДанные.Вставить("СтатусыСерийНоменклатуры", СерииНоменклатурыУНФ.СтатусСерийНоменклатурыПриИзменении(СтруктураДанные));
	Иначе
		СтруктураДанные.Вставить("СтатусыСерийНоменклатуры", 0);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

// Получает набор данных с сервера для процедуры ЕдиницаИзмеренияПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ТекущаяЕдиницаИзмерения = Неопределено, ЕдиницаИзмерения = Неопределено)
	
	СтруктураДанные = Новый Структура;
	
	Если ТекущаяЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("ТекущийКоэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("ТекущийКоэффициент", ТекущаяЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Если ЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("Коэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеЕдиницаИзмеренияПриИзменении()	

// Устанавливает видимость ячейки.
//
&НаКлиенте
Процедура УправлениеФормой()
	
	Если ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) 
		И НЕ ОрдерныйСклад 
		И НЕ ТипСтруктурнойЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Розница") 
		И УчетПоЯчейкам Тогда
		
		Элементы.Ячейка.Видимость = Истина;
		Элементы.СтруктурнаяЕдиница.АвтоМаксимальнаяШирина = Ложь;
	Иначе
		Элементы.Ячейка.Видимость = Ложь;
		Элементы.СтруктурнаяЕдиница.АвтоМаксимальнаяШирина = Истина;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьЯчейки()	

// ПодключаемоеОборудование
// Процедура получает данные по штрихкодам.
//
&НаСервереБезКонтекста
Процедура ПолучитьДанныеПоШтрихКодам(СтруктураДанные)
	
	// Преобразование весовых штрихкодов.
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		РегистрыСведений.ШтрихкодыНоменклатуры.ПреобразоватьВесовойШтрихкод(ТекШтрихкод);
		
	КонецЦикла;
	
	ДанныеПоШтрихКодам = РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьДанныеПоШтрихкодамВМассиве(СтруктураДанные.МассивШтрихкодов);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		МассивДанныхШтрихкода = ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если МассивДанныхШтрихкода <> Неопределено
			И МассивДанныхШтрихкода.Количество() <> 0 Тогда
			
			Для Каждого ДанныеШтрихкода Из МассивДанныхШтрихкода Цикл
				
				СтруктураДанныеНоменклатуры = Новый Структура();
				СтруктураДанныеНоменклатуры.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
				СтруктураДанныеНоменклатуры.Вставить("ТипНоменклатуры", ДанныеШтрихкода.Номенклатура.ТипНоменклатуры);
				ДанныеШтрихкода.Вставить("СтруктураДанныеНоменклатуры", ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанныеНоменклатуры));
				
				Если НЕ ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения) Тогда
					ДанныеШтрихкода.ЕдиницаИзмерения  = ДанныеШтрихкода.Номенклатура.ЕдиницаИзмерения;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанные.Вставить("ДанныеПоШтрихКодам", ДанныеПоШтрихКодам);
	
	Для каждого парам Из Метаданные.Документы.ИнвентаризацияЗапасов.ТабличныеЧасти.Запасы.Реквизиты.Номенклатура.ПараметрыВыбора Цикл
		Если парам.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(парам.Значение)=Тип("ФиксированныйМассив") Тогда
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", парам.Значение);
			Иначе
			    МассивТипов = Новый Массив;
				МассивТипов.Добавить(парам.Значение);
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", МассивТипов);
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПолучитьДанныеПоШтрихКодам()

&НаКлиенте
Функция ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрихкодов)
	
	НеизвестныеШтрихкоды = Новый Массив;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	
	Если ТипЗнч(ДанныеШтрихкодов) = Тип("Массив") Тогда
		МассивШтрихкодов = ДанныеШтрихкодов;
	Иначе
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(ДанныеШтрихкодов);
	КонецЕсли;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("МассивШтрихкодов", МассивШтрихкодов);
	ПолучитьДанныеПоШтрихКодам(СтруктураДанные);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		ДанныеШтрихкода = СтруктураДанные.ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если ДанныеШтрихкода.Количество() > 1 Тогда
			
			ПараметрыОткрытия = Новый Структура("МассивНоменклатуры, ТекШтрихкод", ДанныеШтрихкода, ТекШтрихкод);
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьПоДаннымШтрихкодовЗавершение", ЭтотОбъект, Новый Структура("СтруктураДанные",СтруктураДанные));
			
			ОткрытьФорму("РегистрСведений.ШтрихкодыНоменклатуры.Форма.ДублиНоменклатурыПоШтрихКодам", ПараметрыОткрытия, ЭтаФорма,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
			Продолжить;
		КонецЕсли;
		
		Если ДанныеШтрихкода <> Неопределено
			И ДанныеШтрихкода.Количество() = 0 Тогда
			НеизвестныеШтрихкоды.Добавить(ТекШтрихкод);
		ИначеЕсли СтруктураДанные.ОтборТипНоменклатуры.Найти(ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ТипНоменклатуры) = Неопределено 
			ИЛИ ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ЭтоНабор Тогда
			СвойстваШтрихКода = Новый Структура;
			СвойстваШтрихКода.Вставить("Штрихкод", ТекШтрихкод.Штрихкод);
			СвойстваШтрихКода.Вставить("Номенклатура", ДанныеШтрихкода[0].Номенклатура);
			СвойстваШтрихКода.Вставить("ТипНоменклатуры", ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ТипНоменклатуры);
			СвойстваШтрихКода.Вставить("ЭтоНабор", ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ЭтоНабор);
			ШтрихкодыНекорректногоТипа.Добавить(СвойстваШтрихКода);
		Иначе
			
			ДанныеШтрихкода = ДанныеШтрихкода[0];
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
			ПараметрыОтбора.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
			ПараметрыОтбора.Вставить("Партия", ДанныеШтрихкода.Партия);
			ПараметрыОтбора.Вставить("ЕдиницаИзмерения", ДанныеШтрихкода.ЕдиницаИзмерения);
			
			МассивСтрокТЧ = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
			Если МассивСтрокТЧ.Количество() = 0 Тогда
				
				НоваяСтрока = Объект.Запасы.Добавить();
				НоваяСтрока.Номенклатура = ДанныеШтрихкода.Номенклатура;
				НоваяСтрока.Характеристика = ДанныеШтрихкода.Характеристика;
				НоваяСтрока.Партия = ДанныеШтрихкода.Партия;
				НоваяСтрока.Количество = ТекШтрихкод.Количество;
				НоваяСтрока.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения), ДанныеШтрихкода.ЕдиницаИзмерения, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЕдиницаИзмерения);
				НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
				Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
				
				// Расчет отклонения.
				НоваяСтрока.Отклонение = НоваяСтрока.Количество - НоваяСтрока.КоличествоУчет;
				
			Иначе
				
				НоваяСтрока = МассивСтрокТЧ[0];
				НоваяСтрока.Количество = НоваяСтрока.Количество + ТекШтрихкод.Количество;
				НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
				Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
				
				// Расчет отклонения.
				НоваяСтрока.Отклонение = НоваяСтрока.Количество - НоваяСтрока.КоличествоУчет;
				
			КонецЕсли;
			
			Если ДанныеШтрихкода.Свойство("Серия") И ЗначениеЗаполнено(ДанныеШтрихкода.Серия) Тогда
				СерииНоменклатурыУНФКлиентСервер.ДобавитьСерияВСтроку(НоваяСтрока, ДанныеШтрихкода.Серия, Объект);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа",НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа);КонецФункции // ЗаполнитьПоДаннымШтрихкодов()

&НаКлиенте
Процедура ЗаполнитьПоДаннымШтрихкодовЗавершение(СтруктураНоменклатуры, Параметры) Экспорт
	
	Если СтруктураНоменклатуры = Неопределено Тогда Возврат КонецЕсли;
	
	ДанныеШтрихкода = СтруктураНоменклатуры.МассивНоменклатуры[0];
	ТекШтрихкод = СтруктураНоменклатуры.ТекШтрихкод;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	СтруктураДанные = Параметры.СтруктураДанные;
	
	Если СтруктураДанные.ОтборТипНоменклатуры.Найти(ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры)
		= Неопределено Или ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоНабор Тогда
		СвойстваШтрихКода = Новый Структура;
		СвойстваШтрихКода.Вставить("Штрихкод", ТекШтрихкод.Штрихкод);
		СвойстваШтрихКода.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
		СвойстваШтрихКода.Вставить("ТипНоменклатуры", ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры);
		СвойстваШтрихКода.Вставить("ЭтоНабор", ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоНабор);
		ШтрихкодыНекорректногоТипа.Добавить(СвойстваШтрихКода);
	Иначе
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
		ПараметрыОтбора.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
		ПараметрыОтбора.Вставить("Партия", ДанныеШтрихкода.Партия);
		ПараметрыОтбора.Вставить("ЕдиницаИзмерения", ДанныеШтрихкода.ЕдиницаИзмерения);
		
		МассивСтрокТЧ = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		Если МассивСтрокТЧ.Количество() = 0 Тогда
			
			НоваяСтрока = Объект.Запасы.Добавить();
			НоваяСтрока.Номенклатура = ДанныеШтрихкода.Номенклатура;
			НоваяСтрока.Характеристика = ДанныеШтрихкода.Характеристика;
			НоваяСтрока.Партия = ДанныеШтрихкода.Партия;
			НоваяСтрока.Количество = ТекШтрихкод.Количество;
			НоваяСтрока.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения), ДанныеШтрихкода.ЕдиницаИзмерения, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЕдиницаИзмерения);
			НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
			Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
			
			// Расчет отклонения.
			НоваяСтрока.Отклонение = НоваяСтрока.Количество - НоваяСтрока.КоличествоУчет;
			
		Иначе
			
			НоваяСтрока = МассивСтрокТЧ[0];
			НоваяСтрока.Количество = НоваяСтрока.Количество + ТекШтрихкод.Количество;
			НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
			Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
			
			// Расчет отклонения.
			НоваяСтрока.Отклонение = НоваяСтрока.Количество - НоваяСтрока.КоличествоУчет;
			
		КонецЕсли;
		
		Если ДанныеШтрихкода.Свойство("Серия") И ЗначениеЗаполнено(ДанныеШтрихкода.Серия) Тогда
			СерииНоменклатурыУНФКлиентСервер.ДобавитьСерияВСтроку(НоваяСтрока, ДанныеШтрихкода.Серия, Объект);
		КонецЕсли;
		
	КонецЕсли;
	
	ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	ЗаполнитьПризнакиИспользованияХарактеристик();
	
КонецПроцедуры

// Процедура обрабатывает полученные штрихкоды.
//
&НаКлиенте
Процедура ПолученыШтрихкоды(ДанныеШтрихкодов) Экспорт
	
	Модифицированность = Истина;
	
	НеДобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрихкодов);
	НеизвестныеШтрихкоды		= НеДобавленныеШтрихкоды.НеизвестныеШтрихкоды;
	ШтрихкодыНекорректногоТипа	= НеДобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
	
	ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	
	Если НеизвестныеШтрихкоды.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПолученыШтрихкодыЗавершение", ЭтотОбъект, НеизвестныеШтрихкоды);
		
		ОткрытьФорму(
			"РегистрСведений.ШтрихкодыНоменклатуры.Форма.РегистрацияШтрихкодовНоменклатуры",
			Новый Структура("НеизвестныеШтрихкоды", НеизвестныеШтрихкоды), ЭтотОбъект,,,,Оповещение);
		
		Возврат;
		
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры // ПолученыШтрихкоды()

&НаКлиенте
Процедура ПолученыШтрихкодыЗавершение(ВозвращаемыеПараметры, Параметры) Экспорт
	
	НеизвестныеШтрихкоды = Параметры;
	
	Если ВозвращаемыеПараметры <> Неопределено Тогда
		
		МассивШтрихкодов = Новый Массив;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ЗарегистрированныеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ПолученыНовыеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		НеДобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(МассивШтрихкодов);
		НеизвестныеШтрихкоды		= НеДобавленныеШтрихкоды.НеизвестныеШтрихкоды;
		ШтрихкодыНекорректногоТипа	= НеДобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
		ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды) Экспорт
	
	Для каждого ТекНеизвестныйШтрихкод Из НеизвестныеШтрихкоды Цикл
		
		СтрокаСообщения = НСтр("ru = 'Данные по штрихкоду не найдены: %1%; количество: %2%'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНеизвестныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНеизвестныйШтрихкод.Количество);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
	ЗаполнитьПризнакиИспользованияХарактеристик();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакиИспользованияХарактеристик()
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа) Экспорт
	
	Для каждого ТекНекорректныйШтрихкод Из ШтрихкодыНекорректногоТипа Цикл
		
		СтрокаСообщения = НСтр("ru = 'Найденная по штрихкоду %1% номенклатура -%2%- имеет тип %3%, который не подходит для этой табличной части'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНекорректныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНекорректныйШтрихкод.Номенклатура);
		Если ТекНекорректныйШтрихкод.Свойство("ЭтоНабор") И ТекНекорректныйШтрихкод.ЭтоНабор Тогда
			// Наборы
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", НСтр("ru = 'Набор'"));
		Иначе
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", ТекНекорректныйШтрихкод.ТипНоменклатуры);
		КонецЕсли; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
КонецПроцедуры

// Конец ПодключаемоеОборудование

// Выполняет пересчет цен табличной части документа.
//
&НаКлиенте
Процедура ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен(ВидЦен)
	
	СтруктураДанных = Новый Структура;
	ТабличнаяЧастьДокумента = Новый Массив;
	
	СтруктураДанных.Вставить("Дата", Объект.Дата);
	СтруктураДанных.Вставить("Организация", Компания);
	СтруктураДанных.Вставить("ВидЦен", ВидЦен);
	СтруктураДанных.Вставить("ВалютаДокумента", НациональнаяВалюта);
	
	Для каждого СтрокаТЧ Из Объект.Запасы Цикл
		
		СтрокаТЧ.Цена = 0;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
			Продолжить;
		КонецЕсли; 
		
		СтрокаТабличнойЧасти = Новый Структура();
		СтрокаТабличнойЧасти.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
		СтрокаТабличнойЧасти.Вставить("Характеристика", СтрокаТЧ.Характеристика);
		СтрокаТабличнойЧасти.Вставить("ЕдиницаИзмерения", СтрокаТЧ.ЕдиницаИзмерения);
		СтрокаТабличнойЧасти.Вставить("Цена", 0);
;
		
		ТабличнаяЧастьДокумента.Добавить(СтрокаТабличнойЧасти);
		
	КонецЦикла;
	
	ЦенообразованиеСервер.ПолучитьЦеныТабличнойЧастиПоВидуЦен(СтруктураДанных, ТабличнаяЧастьДокумента);
	
	Для каждого СтрокаТЧ Из ТабличнаяЧастьДокумента Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", СтрокаТЧ.Характеристика);
		СтруктураПоиска.Вставить("ЕдиницаИзмерения", СтрокаТЧ.ЕдиницаИзмерения);
		СтруктураПоиска.Вставить("Пометка",Истина);
		РезультатПоиска = Объект.Запасы.НайтиСтроки(СтруктураПоиска);
		
		Для каждого СтрокаРезультат Из РезультатПоиска Цикл
			
			СтрокаРезультат.Цена = СтрокаТЧ.Цена;
			СтрокаРезультат.Сумма = СтрокаРезультат.Количество * СтрокаРезультат.Цена;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен()


// ИнтеграцияГосИС
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормИСУНФКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры
// Конец ИнтеграцияГосИС

&НаКлиенте
Процедура ОткрытьФормуРеквизитыПечати()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзмененияРеквизитовПечати", ЭтотОбъект);
	ОткрытьФорму("Обработка.РеквизитыПечати.Форма.РеквизитыПечатиИнвентаризацияЗапасов", Новый Структура("КонтекстПечати", Объект), ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНастройкиИзмененияКоличества()
	
	НастройкиКоличества = ДокументыУНФКлиентСервер.ПараметрыСбросаКоличества();
	ДокументыУНФ.ЗаполнитьНастройкиКоличества(НастройкиКоличества);
	Возврат НастройкиКоличества;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область КопированиеСтрокТабличныхЧастей

// Процедура - обработчик нажатия кнопки Копировать строки в ТЧ Запасы.
//
&НаКлиенте
Процедура ЗапасыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Запасы");
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки Вставить строки в ТЧ Запасы.
//
&НаКлиенте
Процедура ЗапасыВставитьСтроки(Команда)
	
	ВставитьСтроки("Запасы");
	
КонецПроцедуры

// Вызывает процедуру копирования строк и оповещает пользователя о количестве скопированных.
//
&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяТЧ].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

// Вызывает процедуру вставки строк и оповещает пользователя о количестве вставленных.
//
&НаКлиенте
Процедура ВставитьСтроки(ИмяТЧ)
	
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных);
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

// Выполняет копирование выделенных строк в буфер обмена.
//
&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

// Вставляет скопированные строки из буфера обмена в выбранную табличную часть.
//
&НаСервере
Процедура ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных)
	
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных);
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
КонецПроцедуры

// Обрабатывает вставленные строки.
//
&НаСервере
Процедура ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	
	Для Итератор = 1 По КоличествоВставленных Цикл
		
		Строка = Объект[ИмяТЧ][Количество - Итератор];
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Номенклатура", Строка.Номенклатура);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		
		Если НЕ ЗначениеЗаполнено(Строка.ЕдиницаИзмерения) Тогда
			Строка.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСПодбором

// Процедура - обработчик события Действие команды Подбор
//
&НаКлиенте
Процедура Подбор(Команда)
	
	ИмяТабличнойЧасти 	= "Запасы";
	ПодборНоменклатурыВДокументахКлиент.ОткрытьФормуПодбораНоменклатуры(ЭтотОбъект, ИмяТабличнойЧасти);
	
КонецПроцедуры // ПодборВыполнить()

// Функция получает список товаров из временного хранилища
//
&НаСервере
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		
		// Расчет отклонения.
		НоваяСтрока.Отклонение = НоваяСтрока.Количество - НоваяСтрока.КоличествоУчет;
		
		Если ИмяТабличнойЧасти = "Запасы" Тогда
			
			ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтрокаЗагрузки.Номенклатура);
			
			НоваяСтрока.Характеристика = ?(ЗначениеЗаполнено(СтрокаЗагрузки.Характеристика), СтрокаЗагрузки.Характеристика, ЗначенияПоУмолчанию);
			
			НоваяСтрока.ПроверятьЗаполнениеХарактеристики = СтрокаЗагрузки.Номенклатура.ПроверятьЗаполнениеХарактеристики;
			НоваяСтрока.ИспользоватьХарактеристики = СтрокаЗагрузки.Номенклатура.ИспользоватьХарактеристики;
			НоваяСтрока.ЗаполнениеХарактеристикиПроверено = Истина;
			
			//Партии
			
			СтатусПартии = Новый СписокЗначений;
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ОтветственноеХранение"));
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ТоварыНаКомиссии"));
			
			НоваяСтрока.ИспользоватьПартии = СтрокаЗагрузки.Номенклатура.ИспользоватьПартии;
			НоваяСтрока.ПроверятьЗаполнениеПартий = СтрокаЗагрузки.Номенклатура.ПроверятьЗаполнениеПартий;
			
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтрокаЗагрузки.Номенклатура,СтатусПартии);
			
			НоваяСтрока.Партия = ?(ЗначениеЗаполнено(СтрокаЗагрузки.Партия), СтрокаЗагрузки.Партия, ЗначенияПартииПоУмолчанию);
			// Конец Партии
			
			// Серии номенклатуры
			Если ЗначениеЗаполнено(СтрокаЗагрузки.Номенклатура) И СтрокаЗагрузки.Номенклатура.ИспользоватьСерииНоменклатуры Тогда
				СтруктураДанные = Новый Структура;
				СтруктураДанные.Вставить("Номенклатура", СтрокаЗагрузки.Номенклатура);
				СерииНоменклатурыУНФКлиентСервер.ДополнитьСтруктуруДаннымиДляПолученияСерийНоменклатуры(Объект, СтруктураДанные);
				НоваяСтрока.СтатусыСерийНоменклатуры = СерииНоменклатурыУНФ.СтатусСерийНоменклатурыПриИзменении(СтруктураДанные);
			Иначе
				НоваяСтрока.СтатусыСерийНоменклатуры = 0;
			КонецЕсли;
			
			ТабличныеЧастиУНФКлиентСервер.ЗаполнитьКлючСвязи(Объект[ИмяТабличнойЧасти], НоваяСтрока, "КлючСвязи");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

// ПодключаемоеОборудование
// Процедура - обработчик команды командной панели табличной части.
//
&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ТекШтрихкод = "";
	
	ОбработкаЗавершения = Новый ОписаниеОповещения(
		"ПоискПоШтрихкодуЗавершение",
		ЭтотОбъект, 
		Новый Структура("ТекШтрихкод", ТекШтрихкод));
	
	#Если МобильныйКлиент Тогда
		ОткрытьФорму("ОбщаяФорма.ФормаПоискаПоШтрихкоду",,,,,,ОбработкаЗавершения);
	#Иначе
		ПоказатьВводЗначения(ОбработкаЗавершения, ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекШтрихкод = ?(Результат = Неопределено, СокрЛП(ДополнительныеПараметры.ТекШтрихкод), СокрЛП(Результат));
	
	
	Если НЕ ПустаяСтрока(ТекШтрихкод) Тогда
		ПолученыШтрихкоды(Новый Структура("Штрихкод, Количество", ТекШтрихкод, 1));
	КонецЕсли;
	
КонецПроцедуры // ПоискПоШтрихкоду()

// Процедура - обработчик события Действие команды ПолучитьВес
//
&НаКлиенте
Процедура ПолучитьВес(Команда)
	
ПодключаемоеОборудованиеУНФКлиент.ПолучениеВесаСЭлектронныхВесовДляТабличнойЧасти(ЭтотОбъект, "Запасы");
	
КонецПроцедуры // ПолучитьВес()

&НаКлиенте
Процедура ПолучитьВесЗавершение(Параметры) Экспорт
	
	СтрокаТабличнойЧасти = Параметры;
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	
	// Расчет отклонения.
	СтрокаТабличнойЧасти.Отклонение = СтрокаТабличнойЧасти.Количество - СтрокаТабличнойЧасти.КоличествоУчет;
	
КонецПроцедуры

// Процедура - обработчик команды ЗагрузитьДанныеИзТСД.
//
&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучитьДанныеИзТСД(ЭтотОбъект);
	
КонецПроцедуры // ЗагрузитьДанныеИзТСД()

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяТаблицыВыборки", "Запасы");
	
	ПодключаемоеОборудованиеУНФКлиент.ВыгрузитьДокументВТСД(ЭтотОбъект, Истина, ДополнительныеПараметры);
	
КонецПроцедуры

// Конец ПодключаемоеОборудование

#КонецОбласти

#Область РаботаССериямиНоменклатуры

&НаКлиенте
Процедура ЗапасыСерииНоменклатурыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	ОткрытьПодборСерииНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодборСерииНоменклатуры()
	
	ТекущиеДанныеИдентификатор = Элементы.Запасы.ТекущиеДанные.ПолучитьИдентификатор();
	ПараметрыСерийНоменклатуры = ПараметрыПодбораСерийНоменклатуры(ТекущиеДанныеИдентификатор);
	
	ОткрытьФорму("Обработка.ПодборСерийНоменклатуры.Форма", ПараметрыСерийНоменклатуры, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСерииНоменклатурыИзХранилища(АдресВоВременномХранилище, КлючСтроки)
	
	Модифицированность = Истина;
	Возврат СерииНоменклатурыУНФ.ПолучитьСерииНоменклатурыИзХранилища(Объект, АдресВоВременномХранилище, КлючСтроки);
	
КонецФункции

&НаСервере
Функция ПараметрыПодбораСерийНоменклатуры(ТекущиеДанныеИдентификатор)

	Возврат СерииНоменклатурыУНФ.ПараметрыПодбораСерийНоменклатуры(Объект, ЭтотОбъект.УникальныйИдентификатор,
		ТекущиеДанныеИдентификатор, Ложь);

КонецФункции
// Конец ПодключаемоеОборудование

#КонецОбласти

#Область ОбработчикиБиблиотек

&НаСервере
Функция ПоместитьНастройкиСКДВоВременноеХранилище()
	Возврат ПоместитьВоВременноеХранилище(НастройкаКомпановкиДанных.ПолучитьНастройки(), УникальныйИдентификатор);
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОтбору(Команда)
	// Вставить содержимое обработчика. 
	Объект.ИмяСхемыКомпоновкиДанных = "ТиповаяСхема" ;    
	СхемаКомпоновкиДанныхПриИзмененииНаСервере(Объект.СхемаСКДМодифицированаВручную);
	АдресСхемыДляРедактирования = ПоместитьКопиюСхемыВоВременноеХранилище(АдресСхемыКомпановкиДанных, УникальныйИдентификатор);
	ПараметрыФормы = Новый Структура;
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр(
		"ru = 'Настройте схему компоновки данных для Инвентаризации '");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПомещатьНастройкиВСхемуКомпоновкиДанных", Ложь);
	ПараметрыФормы.Вставить("РедактироватьСхемуКомпоновкиДанных", Ложь);
	ПараметрыФормы.Вставить("ЗагрузитьСхемуИзФайла", Ложь);
	ПараметрыФормы.Вставить("НастраиватьОтбор", Истина);
	ПараметрыФормы.Вставить("НастраиватьПараметры", Ложь);
	ПараметрыФормы.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыФормы.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыДляРедактирования);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных", ПоместитьНастройкиСКДВоВременноеХранилище());
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	
	ДополнительныеПараметры = Новый Структура("АдресСхемыДляРедактирования", АдресСхемыДляРедактирования);
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение", ЭтотОбъект, ДополнительныеПараметры);

	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных", ПараметрыФормы, , , , , ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры 

&НаКлиенте
Процедура РедактироватьСхемуКомпоновкиДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт

	РедактироватьСхемуКомпоновкиДанныхЗавершениеНаСервере(Результат, ДополнительныеПараметры);
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПоместитьКопиюСхемыВоВременноеХранилище(АдресСхемы, УникальныйИдентификатор)
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
	Возврат ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор)
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоПроизвольномуОтбору(Команда)
	// Вставить содержимое обработчика. 
	Объект.ИмяСхемыКомпоновкиДанных = "ПроизвольныйОтбор" ;  

	Объект.СхемаСКДМодифицированаВручную = Истина;
	АдресСхемыДляРедактирования = ПоместитьКопиюСхемыВоВременноеХранилище(АдресСхемыКомпановкиДанных, УникальныйИдентификатор);
	
	ПараметрыФормы = Новый Структура;
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр(
		"ru = 'Настройте схему компоновки данных для Инвентаризации '");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПомещатьНастройкиВСхемуКомпоновкиДанных", Ложь);
	ПараметрыФормы.Вставить("РедактироватьСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("ЗагрузитьСхемуИзФайла", Истина);
	ПараметрыФормы.Вставить("НастраиватьОтбор", Истина);
	ПараметрыФормы.Вставить("НастраиватьПараметры", Истина);
	ПараметрыФормы.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыФормы.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыДляРедактирования);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных", ПоместитьНастройкиСКДВоВременноеХранилище());
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	
	ДополнительныеПараметры = Новый Структура("АдресСхемыДляРедактирования", АдресСхемыДляРедактирования);
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение", ЭтотОбъект, ДополнительныеПараметры);

	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных", ПараметрыФормы, , , , , ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИтогИзлишкиНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка 		= Ложь;
	Элементы.Запасы.ОтборСтрок 	= ?(ИзлишкиОтбор=Истина,Неопределено,Новый ФиксированнаяСтруктура("ИзлишкиСоответствуетОтбору",истина)); 
	ИзлишкиОтбор 				= не ИзлишкиОтбор;
	
	Если ИзлишкиОтбор = Истина Тогда
		Элементы.ГруппаИзлишки.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ФонАктивногоЗначенияОтбора");
	Иначе 
		Элементы.ГруппаИзлишки.ЦветФона = Новый Цвет();
	КонецЕсли;
	
	ОбработатьЭлементыСтиля(НедостачаОтбор,ОтклонениеОтбор,"ГруппаНедостача","ГруппаОтклонение");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИтогНедостачаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка 		= Ложь;
	Элементы.Запасы.ОтборСтрок 	= ?(НедостачаОтбор=Истина,Неопределено,Новый ФиксированнаяСтруктура("НедостачаСоответствуетОтбору",истина));    
	НедостачаОтбор 				= не НедостачаОтбор;
	
	Если НедостачаОтбор = Истина Тогда
		Элементы.ГруппаНедостача.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ФонАктивногоЗначенияОтбора");
	Иначе 
		Элементы.ГруппаНедостача.ЦветФона = Новый Цвет()
	КонецЕсли;
	
	ОбработатьЭлементыСтиля(ИзлишкиОтбор,ОтклонениеОтбор,"ГруппаИзлишки","ГруппаОтклонение");
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИтогОтклонениеНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка 		= Ложь;
	Элементы.Запасы.ОтборСтрок 	= ?(ОтклонениеОтбор=Истина,Неопределено,Новый ФиксированнаяСтруктура("ОтклонениеСоответствуетОтбору",истина));
	ОтклонениеОтбор 			= не ОтклонениеОтбор;
	
	Если ОтклонениеОтбор = Истина Тогда
		Элементы.ГруппаОтклонение.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ФонАктивногоЗначенияОтбора");
	Иначе 
		Элементы.ГруппаОтклонение.ЦветФона = Новый Цвет();
	КонецЕсли;
	ОбработатьЭлементыСтиля(ИзлишкиОтбор,НедостачаОтбор,"ГруппаИзлишки","ГруппаНедостача");
	
КонецПроцедуры  

&НаКлиенте
Процедура ОбработатьЭлементыСтиля(Отбор1,Отбор2,Группа1,Группа2)
	Отбор1 				= Ложь; 
	Элементы[Группа1].ЦветФона = Новый Цвет();
	Отбор2 				= Ложь;  
	Элементы[Группа2].ЦветФона = Новый Цвет();
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	ВыделитьЭлементы(Истина)
КонецПроцедуры


&НаКлиенте
Процедура ВыделитьЭлементы(Выделение)
	
	Для Каждого Элемент Из Объект.Запасы Цикл 
		Если Элементы.Запасы.ПроверитьСтроку(Элемент.ПолучитьИдентификатор()) Тогда
		Элемент.Пометка = Выделение; 
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ЗапасыСнятьВыделение.Видимость = Выделение;
	Элементы.ЗапасыВыделитьВсе.Видимость = НЕ Выделение;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделение(Команда)
	ВыделитьЭлементы(Ложь)
КонецПроцедуры  


&НаКлиенте 
Процедура КомандаЗаполнитьФактическиеДанныеУчетнымиЗавершение(Результат, ДополнительныеПараметры) Экспорт
Ответ = Результат;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	КомандаЗаполнитьФактическиеДанныеУчетнымиФрагмент();  
	
	Элементы.Запасы.ОтборСтрок 	= Неопределено; 
	ИзлишкиОтбор 				= Ложь;
	НедостачаОтбор 				= Ложь;
	ОтклонениеОтбор				= Ложь;
	Элементы.ГруппаИзлишки.ЦветФона = Новый Цвет(); 
	Элементы.ГруппаНедостача.ЦветФона = Новый Цвет();
	Элементы.ГруппаНедостача.ЦветФона = Новый Цвет();


КонецПроцедуры  

&НаКлиенте
Процедура КомандаЗаполнитьФактическиеДанныеУчетнымиФрагмент()

	Перем СтрокаТабличнойЧасти;

	Для Каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
    Если СтрокаТабличнойЧасти.Пометка Тогда
		СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.КоличествоУчет;
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.СуммаУчет;
		СтрокаТабличнойЧасти.Отклонение = 0;
		СтрокаТабличнойЧасти.Излишки = 0;
		СтрокаТабличнойЧасти.Недостача = 0;
		СтрокаТабличнойЧасти.ИзлишкиСоответствуетОтбору = Ложь; 
		СтрокаТабличнойЧасти.НедостачаСоответствуетОтбору = Ложь;  
		СтрокаТабличнойЧасти.ОтклонениеСоответствуетОтбору = Ложь; 
	КонецЕсли;
	КонецЦикла;

	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИтогИзлишкиПриИзменении(Элемент)

	Если Объект.Запасы.Итог("Излишки") = 0 Тогда
		Элементы.Запасы.ОтборСтрок 	= Неопределено; 
		ИзлишкиОтбор 				= не ИзлишкиОтбор;
	
		Если ИзлишкиОтбор = Истина Тогда
			Элементы.ГруппаИзлишки.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ФонАктивногоЗначенияОтбора");
		Иначе 
			Элементы.ГруппаИзлишки.ЦветФона = Новый Цвет();
		КонецЕсли;
	
		ОбработатьЭлементыСтиля(НедостачаОтбор,ОтклонениеОтбор,"ГруппаНедостача","ГруппаОтклонение");
	КонецЕсли;
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменитьСтроки(Команда)
	
	ПоказатьСкрытьПанельРедактирования(Истина); 
	Для Каждого Элемент Из Объект.Запасы Цикл 
		Элемент.Пометка = Элементы.Запасы.ПроверитьСтроку(Элемент.ПолучитьИдентификатор()); 
	КонецЦикла;
КонецПроцедуры  

&НаСервере
Процедура ПоказатьСкрытьПанельРедактирования(ИзменяетДанные = Неопределено)
	
	Перем СостояниеПерехода;
	ГрупповоеИзменениеСтрокСервер.ПоказатьСкрытьПанельРедактирования(
		ЭтотОбъект,
		НаборЭлементовГрупповогоИзмененияСтрокСервер(),
		СостояниеПерехода,
		ИзменяетДанные);
	
	УправлениеРезервнымиКопиямиТаблицы(СостояниеПерехода, ИзменяетДанные);
	
	Элементы.ЗапасыИзменениеСтрокЗначение.Видимость = Ложь;	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьСписокДействий()
	
	РазрешеноРедактированиеЦенДокументов = УправлениеДоступомУНФ.РазрешеноРедактированиеЦенДокументов();
	
	Действия = Новый Массив;
	Если РазрешеноРедактированиеЦенДокументов Тогда
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ЗаполнитьПоВидуЦен);
	КонецЕсли;
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ЗаполнитьТолькоУчетныеДанные);
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ЗаполнитьФактическиеДанныеУчетными);
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ОбнулитьФактическоеКоличество);
	
	Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора.Очистить();
	Для каждого Действие Из Действия Цикл
		ДействиеОписание = ГрупповоеИзменениеСтрокСервер.ПредставлениеДействия(Действие);
		Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора.Добавить(Действие, ДействиеОписание);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция НаборЭлементовГрупповогоИзмененияСтрокСервер()
	
	НаборЭлементов = Новый Структура();
	НаборЭлементов.Вставить("ИмяТЧ", "Запасы");
	НаборЭлементов.Вставить("ДокументСсылка", Объект.Ссылка);
	НаборЭлементов.Вставить("ПанельРедактирования", Элементы.ГруппаЗапасыИзменениеСтрок);
	НаборЭлементов.Вставить("КнопкаВыполнитьДействие", Элементы.ЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КнопкаИзменитьСтроки", Элементы.ЗапасыИзменитьСтроки);
	НаборЭлементов.Вставить("КолонкаПометка", Элементы.ЗапасыПометка);
	НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтроки);
	НаборЭлементов.Вставить("Действие", ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("ДействиеЭлемент", Элементы.ЗапасыИзменениеСтрокДействие);  
	НаборЭлементов.Вставить("Значение", ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ЗначениеЭлемент", Элементы.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ОбъектИзменений", ЗапасыИзменениеСтрокОбъектИзмененийЭлемент);
	НаборЭлементов.Вставить("КолонкаОбъектИзменений", ?(ЗначениеЗаполнено(ЗапасыИзменениеСтрокОбъектИзмененийЭлемент), Элементы[ЗапасыИзменениеСтрокОбъектИзмененийЭлемент], Неопределено));
	Возврат НаборЭлементов;
	
КонецФункции


&НаСервере
Процедура УправлениеРезервнымиКопиямиТаблицы(СостояниеПерехода, ИзменяетДанные)

	
	
	ГрупповоеИзменениеСтрокСервер.УправлениеРезервнымиКопиями(
		ЭтотОбъект,
		Объект.Запасы,
		ЗапасыРезервнаяКопияТаблицыАдрес,
		СостояниеПерехода,
		ИзменяетДанные);

КонецПроцедуры
	
&НаКлиенте
Процедура ЗапасыВыполнитьДействие(Команда)
	ОбработатьТаблицу();
	//НастроитьОформлениеПанелиРедактирования(4);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьТаблицу()
	
	ОбработатьТаблицуНаСервере();
		
	Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ЗаполнитьТолькоУчетныеДанные") Тогда
		
		ЗаполнитьТолькоУчетныеДанные();
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ЗаполнитьПоВидуЦен") Тогда
		
		ОткрытьФорму("Справочник.ВидыЦен.Форма.ФормаВыбора", , , , , ,
		Новый ОписаниеОповещения("КомандаЗаполнитьПоВидуЦенЗавершение", ЭтотОбъект));
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОбнулитьФактическоеКоличество") Тогда
		
		КомандаОбнулитьКоличествоИСуммуФрагмент();
		Если ИзлишкиОтбор Тогда
			Элементы.Запасы.ОтборСтрок 	= Неопределено; 
			ИзлишкиОтбор 				= Ложь;
			Элементы.ГруппаИзлишки.ЦветФона = Новый Цвет(); 
	    КонецЕсли;
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ЗаполнитьФактическиеДанныеУчетными") Тогда

		КомандаЗаполнитьФактическиеДанныеУчетнымиФрагмент();  
		Элементы.Запасы.ОтборСтрок 	= Неопределено; 
		ИзлишкиОтбор 				= Ложь;
		НедостачаОтбор 				= Ложь;
		ОтклонениеОтбор				= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьТаблицуНаСервере()
	
	ГрупповоеИзменениеСтрокСервер.ОбработатьТаблицу(
		ЭтотОбъект,
		Объект.Запасы,
		ЗапасыИзменениеСтрокДействие,
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит,
		ЗапасыИзменениеСтрокЗначение,
		"ЗапасыНоменклатура");
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыОтменитьИзменения(Команда)
		ПоказатьСкрытьПанельРедактирования();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоИзлишкам(Команда)
	 ИзлишкиОтбор = Истина; 
	 НедостачаОтбор = Ложь;
	 ОтклонениеОтбор = Ложь;
	 УстановитьОтбор()
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьОтбор()
	Если ИзлишкиОтбор Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИзлишкиСоответствуетОтбору", истина);

		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		
		Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура("ИзлишкиСоответствуетОтбору",истина);
//		Элементы.ЗапасыКонтекстноеМенюПоказатьДублиСтроки.Пометка = Истина;
		
		Элементы.ДекорацияОтбор.Заголовок = ИнформацияОбОтборе(
		СтрШаблон(НСтр("ru = 'Излишки (%1)'"), НайденныеСтроки.Количество()));
		
		Элементы.ДекорацияОтбор.Видимость = Истина;
	ИначеЕсли НедостачаОтбор Тогда
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("НедостачаСоответствуетОтбору", истина);

		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		
		Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура("НедостачаСоответствуетОтбору",истина);
//		Элементы.ЗапасыКонтекстноеМенюПоказатьДублиСтроки.Пометка = Истина;
		
		Элементы.ДекорацияОтбор.Заголовок = ИнформацияОбОтборе(
		СтрШаблон(НСтр("ru = 'Недосдачи (%1)'"), НайденныеСтроки.Количество()));
		
		Элементы.ДекорацияОтбор.Видимость = Истина;
	ИначеЕсли ОтклонениеОтбор Тогда
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ОтклонениеСоответствуетОтбору", истина);

		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		
		Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура("ОтклонениеСоответствуетОтбору",истина);
//		Элементы.ЗапасыКонтекстноеМенюПоказатьДублиСтроки.Пометка = Истина;
		
		Элементы.ДекорацияОтбор.Заголовок = ИнформацияОбОтборе(
		СтрШаблон(НСтр("ru = 'Отклонения (%1)'"), НайденныеСтроки.Количество()));
		
		Элементы.ДекорацияОтбор.Видимость = Истина;
	Иначе
		Элементы.Запасы.ОтборСтрок = Неопределено;
		Элементы.ДекорацияОтбор.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнформацияОбОтборе(Знач НаименованиеМетки)
	
	Возврат НоменклатураВДокументахСервер.ИнформацияОбОтборе(НаименованиеМетки);
	
КонецФункции

&НаКлиенте
Процедура ДекорацияОтборОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	 ИзлишкиОтбор = Ложь;
	 НедостачаОтбор = Ложь;
	 ОтклонениеОтбор = Ложь;
	 УстановитьОтбор()
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоНедостачам(Команда)
	 ИзлишкиОтбор = Ложь; 
	 НедостачаОтбор = Истина;
	 ОтклонениеОтбор = Ложь;
	 УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоОтклонениям(Команда)
	 ИзлишкиОтбор = Ложь; 
	 НедостачаОтбор = Ложь;
	 ОтклонениеОтбор = Истина;
	 УстановитьОтбор();
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
