
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КартинкаНетПредпросмотра = БиблиотекаКартинок.КомпактныйСписокПисем;
	КартинкаПредпросмотрСправа = БиблиотекаКартинок.ПредпросмотрСобытияСправа;
	КартинкаПредпросмотрСнизу = БиблиотекаКартинок.ПредпросмотрСобытияСнизу;
	
	КлючОбъекта = "Документ.Событие.Форма.ФормаНастройкаСписка/НастройкиОкна";
	ХранилищеСистемныхНастроек.Удалить(КлючОбъекта, "", ИмяПользователя());
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор;
	
	РазобранныеПараметры = РазобратьПараметры(Параметры);
	ОбработатьПараметры(РазобранныеПараметры);
	
	НастроитьФормуМобильныйКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	СохранитьНастройкиОтборов();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидРасширенныйПриИзменении(Элемент, СтандартнаяОбработка)
	
	ПереключательКомпактный = "";
	ПереключательРасширенный = "Расширенный";
	НастройкаВидСписка = ПереключательРасширенный;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКомпактныйПриИзменении(Элемент, СтандартнаяОбработка)
	
	ПереключательКомпактный = "Компактный";
	ПереключательРасширенный = "";
	НастройкаВидСписка = ПереключательКомпактный;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкиИспользоватьПриИзменении(Элемент, СтандартнаяОбработка)
	
	ПапкиИспользовать = "Использовать";
	ПапкиНеИспользовать = "";
	НастройкаВидПапок = ПапкиИспользовать;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкиНеИспользоватьПриИзменении(Элемент, СтандартнаяОбработка)
	
	ПапкиИспользовать = "";
	ПапкиНеИспользовать = "НеИспользовать";
	НастройкаВидПапок = ПапкиНеИспользовать;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПредпросмотраПисьмаСправаПриИзменении(Элемент, СтандартнаяОбработка)
	
	ВидПредпросмотраПисьмаСправа = "Использовать";
	ВидПредпросмотраПисьмаСнизу = "";
	ВидПредпросмотраПисьмаНет = "";
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПредпросмотраПисьмаСнизуПриИзменении(Элемент, СтандартнаяОбработка)
	
	ВидПредпросмотраПисьмаСправа = "";
	ВидПредпросмотраПисьмаСнизу = "Использовать";
	ВидПредпросмотраПисьмаНет = "";
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПредпросмотраПисьмаНетПриИзменении(Элемент, СтандартнаяОбработка)
	
	ВидПредпросмотраПисьмаСправа = "";
	ВидПредпросмотраПисьмаСнизу = "";
	ВидПредпросмотраПисьмаНет = "Использовать";
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция РазобратьПараметры(Параметры)
	
	Результат = Новый Структура;
	
	ТолькоПредпросмотр = Неопределено;
	Параметры.Свойство("ТолькоПредпросмотр", ТолькоПредпросмотр);
	НеопределеноВЛожь(ТолькоПредпросмотр);
	Результат.Вставить("ТолькоПредпросмотр", ТолькоПредпросмотр);
	
	НастройкаСпискаСобытий = Неопределено;
	Параметры.Свойство("НастройкаСпискаСобытий", НастройкаСпискаСобытий);
	НеопределеноВЛожь(НастройкаСпискаСобытий);
	Результат.Вставить("НастройкаСпискаСобытий", НастройкаСпискаСобытий);
	
	НастройкаСпискаИстории = Неопределено;
	Параметры.Свойство("НастройкаСпискаИстории", НастройкаСпискаИстории);
	НеопределеноВЛожь(НастройкаСпискаИстории);
	Результат.Вставить("НастройкаСпискаИстории", НастройкаСпискаИстории);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбработатьПараметры(РазобранныеПараметры)
	
	Если РазобранныеПараметры.НастройкаСпискаИстории Тогда
		
		ПриСозданииНаСервереДляСпискаИстории();
		
	Иначе
		
		ПриСозданииНаСервереДляОсновногоСписка(РазобранныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуМобильныйКлиент()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПредпросмотр.Видимость = Ложь;
	Элементы.Отступ3.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереДляСпискаИстории()
	
	ТипНастраиваемогоСписка = "ИсторияПереписки";
	
	Заголовок = НСтр("ru = 'Настройка истории переписки'");
	ОтображениеЭлементовТолькоДляПредпросмотра(Истина);
	
	НастройкиСпискаИсторииПереписки = ХранилищеСистемныхНастроек.Загрузить(
		"НастройкиСпискаИсторииПереписки", "ГруппаОтбораСписокПисем_НастройкиСпискаИсторииПереписки");
	
	Если ТипЗнч(НастройкиСпискаИсторииПереписки) = Тип("Структура") Тогда
		
		ВидПредпросмотраПисьма = Неопределено;
		НастройкиСпискаИсторииПереписки.Свойство("ВидПредпросмотраПисьма", ВидПредпросмотраПисьма);
		Если ВидПредпросмотраПисьма = Неопределено Тогда
			ВидПредпросмотраПисьма = "Снизу";
		КонецЕсли;
		
		Если ВидПредпросмотраПисьма = "Снизу" Тогда
			ВидПредпросмотраПисьмаСнизу = "Использовать";
		ИначеЕсли ВидПредпросмотраПисьма = "Справа" Тогда
			ВидПредпросмотраПисьмаСправа = "Использовать";
		Иначе
			ВидПредпросмотраПисьмаНет = "Использовать";
		КонецЕсли;
		
	Иначе
		
		ВидПредпросмотраПисьмаСправа = "Использовать";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереДляОсновногоСписка(РазобранныеПараметры)
	
	ТипНастраиваемогоСписка = "ГлавныйСписок";
	
	Если РазобранныеПараметры.НастройкаСпискаСобытий Тогда
		Заголовок = НСтр("ru = 'Настройки списка событий'");
	КонецЕсли;
	
	ОтображениеЭлементовТолькоДляПредпросмотра(РазобранныеПараметры.ТолькоПредпросмотр);
	
	КартинкаКомпактныйСписок = БиблиотекаКартинок.КомпактныйСписокПисем;
	КартинкаРасширенныйСписок = БиблиотекаКартинок.РасширенныйСписокПисем;
	КартинкаСписокСПапками = БиблиотекаКартинок.ПапкиПисемВСпискеИспользовать;
	КартинкаПапкиНеИспользовать = БиблиотекаКартинок.КомпактныйСписокПисем;
	
	НастройкиСпискаПапок = ХранилищеСистемныхНастроек.Загрузить(
		"НастройкиСпискаПапок", "ГруппаОтбораСписокПисем_НастройкиСпискаПапок");
	
	Если ТипЗнч(НастройкиСпискаПапок) = Тип("Структура") Тогда
		
		НастройкаВидСписка = НастройкиСпискаПапок.ВидСписка;
		НастройкаВидПапок  = НастройкиСпискаПапок.ВидПапок;
		ОтображатьКолонкуВложения = НастройкиСпискаПапок.ОтображатьКолонкуВложения;
		
		Если ОтображатьКолонкуВложения = Неопределено Тогда
			ОтображатьКолонкуВложения = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НастройкаВидСписка) Тогда
			НастройкаВидСписка = "Расширенный";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НастройкаВидПапок) Тогда
			НастройкаВидПапок = "Использовать";
		КонецЕсли;
		
		ПереключательКомпактный = НастройкаВидСписка;
		ПереключательРасширенный = НастройкаВидСписка;
		
		ПапкиИспользовать = НастройкаВидПапок;
		ПапкиНеИспользовать = НастройкаВидПапок;
		
		ВидПредпросмотраПисьма = Неопределено;
		НастройкиСпискаПапок.Свойство("ВидПредпросмотраПисьма", ВидПредпросмотраПисьма);
		Если ВидПредпросмотраПисьма = Неопределено Тогда
			ВидПредпросмотраПисьма = "Снизу";
		КонецЕсли;
		
		Если ВидПредпросмотраПисьма = "Снизу" Тогда
			ВидПредпросмотраПисьмаСнизу = "Использовать";
		ИначеЕсли ВидПредпросмотраПисьма = "Справа" Тогда
			ВидПредпросмотраПисьмаСправа = "Использовать";
		Иначе
			ВидПредпросмотраПисьмаНет = "Использовать";
		КонецЕсли;
		
	Иначе
		
		ПереключательКомпактный = "";
		ПереключательРасширенный = "Расширенный";
		
		ПапкиИспользовать = "Использовать";
		ПапкиНеИспользовать = "";
		
		ОтображатьКолонкуВложения = Истина;
		
		ВидПредпросмотраПисьмаСнизу = "Использовать";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтображениеЭлементовТолькоДляПредпросмотра(ТолькоПредпросмотр)
	
	ВсеЭлементы = Не ТолькоПредпросмотр;
	
	Элементы.ГруппаВидСписка.Видимость = ВсеЭлементы;
	Элементы.ГруппаДеревоПапок.Видимость = ВсеЭлементы;
	Элементы.ГруппаВидимостьПолей.Видимость = ВсеЭлементы;
	Элементы.Отступ1.Видимость = ВсеЭлементы;
	Элементы.Отступ2.Видимость = ВсеЭлементы;
	Элементы.Отступ4.Видимость = ВсеЭлементы;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	Если ВидПредпросмотраПисьмаСнизу = "Использовать" Тогда
		ВидПредпросмотраПисьма = "Снизу";
	ИначеЕсли ВидПредпросмотраПисьмаСправа = "Использовать" Тогда
		ВидПредпросмотраПисьма = "Справа";
	Иначе
		ВидПредпросмотраПисьма = "Нет";
	КонецЕсли;
	
	Если ТипНастраиваемогоСписка = "ГлавныйСписок" Тогда
		СохранитьНастройкиГлавногоСписка(ВидПредпросмотраПисьма);
	Иначе
		СохранитьНастройкиИсторииПереписки(ВидПредпросмотраПисьма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиГлавногоСписка(ВидПредпросмотраПисьма)
	
	НастройкиСпискаПапок = Новый Структура;
	НастройкиСпискаПапок.Вставить("ВидСписка", НастройкаВидСписка);
	НастройкиСпискаПапок.Вставить("ВидПапок", НастройкаВидПапок);
	НастройкиСпискаПапок.Вставить("ОтображатьКолонкуВложения", ОтображатьКолонкуВложения);
	НастройкиСпискаПапок.Вставить("ВидПредпросмотраПисьма", ВидПредпросмотраПисьма);
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		
		ХранилищеСистемныхНастроек.Сохранить("НастройкиСпискаПапок", 
			"ГруппаОтбораСписокПисем_НастройкиСпискаПапок",
			НастройкиСпискаПапок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиИсторииПереписки(ВидПредпросмотраПисьма)
	
	НастройкиСпискаИсторииПереписки = Новый Структура;
	НастройкиСпискаИсторииПереписки.Вставить("ВидПредпросмотраПисьма", ВидПредпросмотраПисьма);
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		
		ХранилищеСистемныхНастроек.Сохранить("НастройкиСпискаИсторииПереписки", 
			"ГруппаОтбораСписокПисем_НастройкиСпискаИсторииПереписки",
			НастройкиСпискаИсторииПереписки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НеопределеноВЛожь(Значение)
	
	Если Значение = Неопределено Тогда
		Значение = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
