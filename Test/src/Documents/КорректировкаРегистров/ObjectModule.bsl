#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбменДаннымиСобытияУНФ.ПропуститьДозаполнениеДокумента(ЭтотОбъект, РежимЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	СсылкаПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
	ПометкаУдаленияЗаписанная = ОписаниеТиповБулево.ПривестиЗначение(СсылкаПометкаУдаления);
	
	Если НЕ ЭтоНовый() И ПометкаУдаленияЗаписанная <> ПометкаУдаления Тогда
		УстановитьАктивностьДвижений(НЕ ПометкаУдаления);
	ИначеЕсли ПометкаУдаления Тогда
		УстановитьАктивностьДвижений(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает признак активности для записей регистров.
//
Процедура УстановитьАктивностьДвижений(ФлагАктивности)
	
	СтруктураПоиска = Новый Структура("Имя","Запасы");
	НайденныеСтроки = ТаблицаРегистров.НайтиСтроки(СтруктураПоиска);
	РегистрЗапасыВыбран = НайденныеСтроки.Количество();
	
	ОбновитьРегистрОстатки = Ложь;
	Если РегистрЗапасыВыбран Тогда
		ТаблицаНоменклатурыДляОбновленияОстатков = ТаблицаНоменклатурыЗапасыПоРегистратору();
		ОбновитьРегистрОстатки = ТаблицаНоменклатурыДляОбновленияОстатков.Количество();
	КонецЕсли;
	
	Для Каждого Движение Из Движения Цикл
		
		Движение.Прочитать();
		Движение.УстановитьАктивность(ФлагАктивности);
		
		Если ТипЗнч(Движение) = Тип("РегистрНакопленияНаборЗаписей.Запасы") И ОбновитьРегистрОстатки Тогда
			Движение.ДополнительныеСвойства.Вставить("ТаблицаНоменклатурыДляОбновленияОстатков", ТаблицаНоменклатурыДляОбновленияОстатков);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // УстановитьАктивностьДвижений()

Функция ТаблицаНоменклатурыЗапасыПоРегистратору()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Запасы.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.Регистратор = &Регистратор
	|	И НЕ Запасы.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли