
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СчетФактура = Параметры.СчетФактура;
	СчетФактураПолученный = Параметры.СчетФактураПолученный;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Прослеживаемые товары %1'"), СчетФактура); 
	Если ТипЗнч(Параметры.ТекущиеДанные) = Тип("Массив") Тогда
		Для Каждого Строка Из Параметры.ТекущиеДанные Цикл
			НоваяСтрока = ДанныеПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	ЗаполнитьЕдиницыИзмеренияНоменклатуры();
	
	Если ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Элементы.СчетФактураПолученный.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("СчетФактура", СчетФактура);
	СтруктураВозврата.Вставить("СчетФактураПолученный", СчетФактураПолученный);
	СтруктураВозврата.Вставить("АдресТаблицыРНПТ", ПоместитьРНПТВХранилище());
	
	Если ПеренестиВДокумент Тогда
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеПрослеживаемости

&НаКлиенте
Процедура ДанныеПрослеживаемостиНоменклатураПриИзменении(Элемент)
	ЗаполнитьЕдиницыИзмеренияНоменклатуры(Элементы.ДанныеПрослеживаемости.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоСчетуФактуре(Команда)
	ЗаполнитьДанныеПрослеживаемости();
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	ПеренестиВДокумент = НЕ ЭтаФорма.ТолькоПросмотр;
	Закрыть(КодВозвратаДиалога.OK);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеПрослеживаемости()
	ДанныеПрослеживаемости.Очистить();
	
	ДанныеЗаполнения = Документы.ЖурналУчетаСчетовФактур.ДанныеЖурналаСчетовФактурПоДокументу(СчетФактура);
	Для Каждого Строка Из ДанныеЗаполнения.СведенияРНПТ Цикл
		Если Строка.СчетФактура = СчетФактура И Строка.СчетФактураПолученный = СчетФактураПолученный Тогда
			НоваяСтрока = ДанныеПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЕдиницыИзмеренияНоменклатуры();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЕдиницыИзмеренияНоменклатуры(ИдентификаторСтроки = Неопределено)
	СписокНоменклатуры = Новый Массив;
	Если ИдентификаторСтроки = Неопределено Тогда
		Для Каждого Строка Из ДанныеПрослеживаемости Цикл
			СписокНоменклатуры.Добавить(Строка.Номенклатура);
		КонецЦикла;
	Иначе
		Строка = ДанныеПрослеживаемости.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Строка <> Неопределено Тогда
			СписокНоменклатуры.Добавить(Строка.Номенклатура);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаНоменклатуры,
	|	Номенклатура.ТоварнаяНоменклатураВЭД.ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокНоменклатуры)";
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Выборка = Запрос.Выполнить().Выбрать();
	ОтборСтрок = Новый Структура("Номенклатура");
	Пока Выборка.Следующий() Цикл
		ОтборСтрок.Номенклатура = Выборка.Номенклатура;
		СтрокиПрослеживаемости = ДанныеПрослеживаемости.НайтиСтроки(ОтборСтрок);
		Для Каждого Строка Из СтрокиПрослеживаемости Цикл
			ЗаполнитьЗначенияСвойств(Строка, Выборка, "ЕдиницаНоменклатуры, ЕдиницаПрослеживаемости");
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПоместитьРНПТВХранилище()
	
	ТаблицаРНПТ = ДанныеПрослеживаемости.Выгрузить();
	Для Каждого СтрокаРНПТ Из ТаблицаРНПТ Цикл
		Если СтрокаРНПТ.ЕдиницаНоменклатуры = СтрокаРНПТ.ЕдиницаПрослеживаемости Тогда
			СтрокаРНПТ.КоличествоПрослеживаемости = СтрокаРНПТ.Количество;
		КонецЕсли;
	КонецЦикла;
	АдресТаблицыРНПТВХранилище = ПоместитьВоВременноеХранилище(ТаблицаРНПТ, УникальныйИдентификатор);
	
	Возврат АдресТаблицыРНПТВХранилище;
КонецФункции

#КонецОбласти