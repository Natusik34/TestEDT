
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СсылкаНаУзелОбмена = Параметры.СсылкаНаУзелОбмена;
	
	ОписаниеУзлаОбмена = ПланыОбмена.ИнтеграцияСМагазинамиСоцСетей.ПолучитьОписаниеУзлаОбмена(СсылкаНаУзелОбмена);
	
	ДатаЗагрузкиОстатков = НачалоДня(ТекущаяДатаСеанса());
	ДатаУстановкиЦен = НачалоДня(ТекущаяДатаСеанса());
	Организация = ОписаниеУзлаОбмена.Организация;
	ВидЦеныНоменклатуры = ОписаниеУзлаОбмена.ВидЦеныНоменклатуры;
	ВидЦеныНоменклатурыСтарая = ОписаниеУзлаОбмена.ВидЦеныНоменклатурыСтарая;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОтображениеНаКлиенте(); 
	
	Элементы.СтраницыТело.ТекущаяСтраница = Элементы.СтраницаНастройка;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовЭлементовФормы

&НаКлиенте
Процедура ЗагружатьЦеныПриИзменении(Элемент)
	ОбновитьОтображениеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗагружатьОстаткиПриИзменении(Элемент)
	ОбновитьОтображениеНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьЗагрузку(Команда)
	
	Отказ = Ложь;
	
	Если ЗагружатьЦены И НЕ ЗначениеЗаполнено(ДатаУстановкиЦен) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обязательное поле ""Дата установки цен"".",,"ДатаУстановкиЦен",,Отказ);
	КонецЕсли;
	
	Если ЗагружатьЦены И НЕ ЗначениеЗаполнено(ВидЦеныНоменклатуры) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обязательное поле ""Вид цены номенклатуры"".",,"ВидЦеныНоменклатуры",,Отказ);
	КонецЕсли;
	
	Если ЗагружатьОстатки И НЕ ЗначениеЗаполнено(ДатаЗагрузкиОстатков) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обязательное поле ""Дата загрузки остатков"".",,"ДатаЗагрузкиОстатков",,Отказ);
	КонецЕсли;
	
	Если ЗагружатьОстатки И НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обязательное поле ""Организация"".",,"Организация",,Отказ);
	КонецЕсли;
	
	Если ЗагружатьОстатки И НЕ ЗначениеЗаполнено(СкладДляПодстановкиВЗаказыИОстатков) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обязательное поле ""Склад"".",,"СкладДляПодстановкиВЗаказыИОстатков",,Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ЗапуститьЗагрузкуВФоне();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОтображениеНаКлиенте()
	
	//Элементы.СтраницыЦены.ТекущаяСтраница
	//= ?(ЗагружатьЦены, Элементы.СтраницаЗагружатьЦены, Элементы.СтраницаНеЗагружатьЦены);
	Элементы.СтраницыЦены.ТекущаяСтраница = Элементы.СтраницаЗагружатьЦены;
	Элементы.СтраницаЗагружатьЦены.Доступность = ЗагружатьЦены;
	
	//Элементы.СтраницыОстатки.ТекущаяСтраница
	//= ?(ЗагружатьОстатки, Элементы.СтраницаЗагружатьОстатки, Элементы.СтраницаНеЗагружатьОстатки);
	
	Элементы.СтраницыОстатки.ТекущаяСтраница = Элементы.СтраницаЗагружатьОстатки;
	Элементы.СтраницаЗагружатьОстатки.Доступность = ЗагружатьОстатки;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьЗагрузкуВФонеСервер() 
	
	ОписаниеУзелОбмена = ПланыОбмена.ИнтеграцияСМагазинамиСоцСетей.ПолучитьОписаниеУзлаОбмена(СсылкаНаУзелОбмена);
	ОписаниеУзелОбмена.Вставить("ИмяСобытия", НаименованиеФоновойЗагрузки());
	
	ОписаниеУзелОбмена.Вставить("ЗагружатьВиртуальныеКаталоги", ЗагружатьВиртуальныеКаталоги);
	
	ОписаниеУзелОбмена.Вставить("ЗагружатьЦены", ЗагружатьЦены);
	ОписаниеУзелОбмена.Вставить("ВидЦеныНоменклатуры", ВидЦеныНоменклатуры);
	ОписаниеУзелОбмена.Вставить("ВидЦеныНоменклатурыСтарая", ВидЦеныНоменклатурыСтарая);
	ОписаниеУзелОбмена.Вставить("ДатаУстановкиЦен", ДатаУстановкиЦен);
	
	ОписаниеУзелОбмена.Вставить("ЗагружатьОстатки", ЗагружатьОстатки);
	ОписаниеУзелОбмена.Вставить("Организация", Организация);
	ОписаниеУзелОбмена.Вставить("СкладДляПодстановкиВЗаказыИОстатков", СкладДляПодстановкиВЗаказыИОстатков);
	ОписаниеУзелОбмена.Вставить("ДатаЗагрузкиОстатков", ДатаЗагрузкиОстатков);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ОписаниеУзелОбмена", ОписаниеУзелОбмена);
	ПараметрыЗадания.Вставить("ПолучатьВсеДанные", Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновойЗагрузки();
	ПараметрыВыполнения.ЗапуститьНеВФоне = Ложь;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Попытка
		Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПланыОбмена.ИнтеграцияСМагазинамиСоцСетей.ЗагрузитьНоменклатуру",
		ПараметрыЗадания,
		ПараметрыВыполнения);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьИсключениеНаСервере(ПредставлениеОшибки);
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

&НаСервере
Процедура ЗаписатьИсключениеНаСервере(Знач ПредставлениеОшибки)
	
	ЗаписьЖурналаРегистрации(НаименованиеФоновойЗагрузки(), УровеньЖурналаРегистрации.Ошибка, , , ПредставлениеОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатВыполненияЗадания(Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		
		Элементы.СтраницыТело.ТекущаяСтраница = Элементы.СтраницаРезультатВыполнения;
		Элементы.ФормаЗакрыть.Заголовок = "Закрыть";
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаЗапуститьЗагрузку.Видимость = Ложь;
		
		Данные = ПолучитьИзВременногоХранилища( Результат.АдресРезультата );
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		СтрокаЛогЗагрузкиССайта = СтрокаЛогЗагрузкиССайта + ?(СтрокаЛогЗагрузкиССайта="","",Символы.ПС) +
		Результат.КраткоеПредставлениеОшибки + " " + Результат.ПодробноеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		
		Элементы.СтраницыТело.ТекущаяСтраница = Элементы.СтраницаРезультатВыполнения;
		
	ИначеЕсли Результат.Статус = "Отменено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Отменено пользователем");
		Элементы.СтраницыТело.ТекущаяСтраница = Элементы.СтраницаРезультатВыполнения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьЗагрузкуВФоне()
	
	Элементы.СтраницыТело.ТекущаяСтраница = Элементы.СтраницаХодВыполнения;
	
	ДлительнаяОперация = ЗапуститьЗагрузкуВФонеСервер();
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОбработатьРезультатВыполненияЗадания(ДлительнаяОперация);
		Возврат;
	КонецЕсли;
	
	ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ВыполнитьДействиеПрогрессВыполнения", ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьДействиеЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ФормаВладелец = ЭтотОбъект;
	ПараметрыОжидания.ТекстСообщения = НаименованиеФоновойЗагрузки();
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда  // отменено пользователем
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатВыполненияЗадания(Результат);
	
КонецПроцедуры 

&НаКлиенте
Функция ПолучитьЗначениеПоля( ПараметрЗначение, ПараметрПуть, ЗначениеПоУмолчанию = Неопределено, ТекстОшибки = "" ) Экспорт
	
	Результат = ЗначениеПоУмолчанию;
	
	ЗначениеПолучаемое = ПараметрЗначение;
	ЧастиПути = СтрРазделить( ПараметрПуть, ".", Ложь );
	Попытка
		
		Для Каждого ЧастьПути Из ЧастиПути Цикл
			ЗначениеПолучаемое = ЗначениеПолучаемое[ ЧастьПути ];
		КонецЦикла;
		
		Результат = ЗначениеПолучаемое;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки( ИнформацияОбОшибке );
		
		ТекстОшибки = "" + ПараметрПуть + ": <" + ЧастьПути + ">: " + КраткоеПредставлениеОшибки;
		
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействиеПрогрессВыполнения(Прогресс, ДополнительныеПараметры) Экспорт
	
	ПрогрессЗагруженныеДанные = ПолучитьЗначениеПоля( Прогресс, "Прогресс.ДополнительныеПараметры.ЗагруженныеДанные", Неопределено );
	Если ТипЗнч( ПрогрессЗагруженныеДанные  ) = Тип( "Массив" ) Тогда
		Для Каждого ПрогрессЗагруженныеДанныеЭлемент Из ПрогрессЗагруженныеДанные Цикл
			ЗагруженныеДанныеСтрока = ЭтотОбъект.ЗагруженныеДанные.Добавить();
			ЗаполнитьЗначенияСвойств( ЗагруженныеДанныеСтрока, ПрогрессЗагруженныеДанныеЭлемент );
			ЗагруженныеДанныеСтрока.Изображения = (ПрогрессЗагруженныеДанныеЭлемент.Изображения.Количество() > 0); 
		КонецЦикла;
		
		Элементы.ЗагруженныеДанныеЦена.Видимость = ЗагружатьЦены;
		Элементы.ЗагруженныеДанныеОстаток.Видимость = ЗагружатьОстатки;
		
	КонецЕсли;
	
	Если Прогресс.Сообщения <> Неопределено Тогда
		Для каждого СообщениеПользователю Из Прогресс.Сообщения Цикл
			//СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(Прогресс.Прогресс) = Тип("Структура") Тогда
		
		ПроцентПрогресса = Прогресс.Прогресс.Процент;
		СтрокаЛогЗагрузкиССайта = Прогресс.Прогресс.Текст;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НаименованиеФоновойЗагрузки() Экспорт
	
	#Если Клиент Тогда
		КодОсновногоЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
	#Иначе
		КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	#КонецЕсли
	
	Возврат НСтр("ru = 'Интеграция ВКонтакте.Первичная загрузка'", КодОсновногоЯзыка);
	
КонецФункции

&НаКлиенте
Процедура ЗагруженныеДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ЗагруженныеДанныеНоменклатура" Тогда
		Значение = ТекущиеДанные.Номенклатура;
	ИначеЕсли Поле.Имя = "ЗагруженныеДанныеХарактеристика" Тогда
		Значение = ТекущиеДанные.Характеристика;
	КонецЕсли;
	
	Если ЗначениеЗаполнено( Значение ) Тогда
		ПоказатьЗначение( , Значение );
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
