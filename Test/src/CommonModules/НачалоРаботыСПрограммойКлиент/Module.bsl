#Область ПрограммныйИнтерфейс

// Показать окно начало работы с программой при начале работы системы
//
// Параметры:
//  ПараметрыРаботыКлиента - См. СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске
// 
// Возвращаемое значение:
//  Булево - значение параметра "ПоказатьОкноНачалоРаботыСПрограммой"
//
Функция ПоказатьОкноНачалоРаботыСПрограммой(ПараметрыРаботыКлиента) Экспорт
	
	Если Не ПараметрыРаботыКлиента.Свойство("ПоказатьОкноНачалоРаботыСПрограммой") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыРаботыКлиента.ПоказатьОкноНачалоРаботыСПрограммой) <> Тип("Булево") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПараметрыРаботыКлиента.ПоказатьОкноНачалоРаботыСПрограммой;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработкаЗакрытияФормыНачалаРаботы(Результат, ПараметрыНачалаРаботы) Экспорт
	Перем ОбработкаЗавершена;

	Если Результат = Неопределено Тогда
		ЗавершитьРаботуСистемы(Ложь, Ложь);
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Если Результат.ДополнительныйПараметр = НачалоРаботыСПрограммойКлиентСервер.ЗначениеДополнительногоПараметра() Тогда
		НачалоРаботыСПрограммойВызовСервера.УстановитьСтандартныйИнтерфейс();
		ОбновитьИнтерфейс();
		Возврат;
	КонецЕсли;

	НачалоРаботыСПрограммойКлиентПереопределяемый.ОбработкаЗакрытияФормыНачалаРаботыНаКлиенте(Результат,
		ПараметрыНачалаРаботы, ОбработкаЗавершена);
	НачалоРаботыСПрограммойВызовСервера.ОбработкаЗакрытияФормыНачалаРаботы(Результат, ПараметрыНачалаРаботы,
		ОбработкаЗавершена);

	Если ОбработкаЗавершена = Истина Тогда

		НачалоРаботыСПрограммойВызовСервера.УстановитьСтандартныйИнтерфейс(Результат.ПользовательИмя);

		Если Не ПараметрыНачалаРаботы.РазделениеВключено И Результат.НачатьРаботу = Истина Тогда

			ДополнительныеПараметрыКоманднойСтроки = "";
			Если Результат.Свойство("ПользовательИмя") Тогда
				ДополнительныеПараметрыКоманднойСтроки = СтрШаблон("/N""%1""", Результат.ПользовательИмя);
			КонецЕсли;

			ЗавершитьРаботуСистемы(Ложь, Истина, ДополнительныеПараметрыКоманднойСтроки);

		Иначе

			ОбновитьИнтерфейс();

		КонецЕсли;

	КонецЕсли;

	Если Результат.НачатьРаботу <> Истина Тогда
		ЗавершитьРаботуСистемы(Ложь, Ложь);
	КонецЕсли;

КонецПроцедуры

#конецОбласти