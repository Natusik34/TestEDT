#Область ПрограммныйИнтерфейс

// Используется для отказа от дозаполнения, чтобы не регистрировать документы к обмену при перепроведении.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект
//  РежимЗаписи - РежимЗаписиДокумента
// 
// Возвращаемое значение:
//  Булево - признак повторного проведения документа.
Функция ПропуститьДозаполнениеДокумента(ДокументОбъект, РежимЗаписи) Экспорт
	ДокументОбъектМодифицированность = ДокументОбъект.Модифицированность();
	ДокументОбъект.ДополнительныеСвойства.Вставить("Модифицированность", ДокументОбъектМодифицированность);
	Если ДокументОбъектМодифицированность Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Не ДокументОбъект.Проведен Тогда
		Возврат Ложь;
	КонецЕсли;
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриРегистрацииОбработчиковЗагрузкиДанных
// 
// Параметры:
//  Контейнер - ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера
//  Объект - КонстантаМенеджерЗначения
//         - СправочникОбъект
//         - ДокументОбъект
//         - БизнесПроцессОбъект
//         - ЗадачаОбъект
//         - ПланСчетовОбъект
//         - ПланОбменаОбъект
//         - ПланВидовХарактеристикОбъект
//         - ПланВидовРасчетаОбъект
//         - РегистрСведенийНаборЗаписей
//         - РегистрНакопленияНаборЗаписей
//         - РегистрБухгалтерииНаборЗаписей
//         - РегистрРасчетаНаборЗаписей
//         - ПоследовательностьНаборЗаписей
//         - ПерерасчетНаборЗаписей
//  Артефакты - Массив Из ОбъектXDTO
//  Отказ - Булево
Процедура ПередЗагрузкойОбъекта(Контейнер, Объект, Артефакты, Отказ) Экспорт
	
	// Отключаем регистрацию дублей контрагентов при загрузке/выгрузке данных в сервисе
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.Контрагенты") Тогда
		Объект.ДополнительныеСвойства.Вставить("РегистрироватьДублиКонтрагентов", Ложь);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.АвтоматическиеСкидки") Тогда
		Объект.ДополнительныеСвойства.Вставить("РегистрироватьСлужебныйАвтоматическиеСкидки", Ложь);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти