
#Область СлужебныйПрограммныйИнтерфейс

Функция ПредставлениеПустогоЗначенияДанныхМП() Экспорт
	
	Возврат "<не заполнено>";
	
КонецФункции

Функция ТипДокументаМП(ДокументЭПД) Экспорт
	
	Результат = "";
	
	Если ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная")
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭТрН") Тогда
		Результат = "ETRN";
	ИначеЕсли ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ") Тогда
		Результат = "EPL"; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТитулПоддерживаетсяМобильнымПриложением(ПрефиксТитула) Экспорт
	
	Возврат ПрефиксТитула = "ТитулПеревозчикаПриемка"
		Или ПрефиксТитула = "ТитулПеревозчикаВыдача";
	
КонецФункции

Функция АтомарныеРолиМП() Экспорт
	
	Результат = Новый Массив;
	
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru='Оформитель'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru='Медработник'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru='Механик'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru='Ответственный за показания одометра'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru='Водитель'"));
	
	Возврат Результат;
	
КонецФункции

Функция ПредставленияРолейМП(Все = Истина, Отбор = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	АтомарныеРоли = АтомарныеРолиМП();
	
	ВсегоРолей = АтомарныеРоли.Количество();
	
	РолиПоддерживаемыеМП = РолиПоддерживаемыеМП();
	
	Если Все = Истина Тогда
		Для СчетчикОбщий = 1 По Pow(2, ВсегоРолей) - 1 Цикл
			СчетчикАтомарный = 0;	
			ПредставлениеМассив = Новый Массив;
			СоответствуетОтбору = Истина;
			Для Каждого Роль Из АтомарныеРоли Цикл
				Если РолиПоддерживаемыеМП.Найти(СчетчикАтомарный) <> Неопределено Тогда				
					РольЧисловая = Pow(2, СчетчикАтомарный);
					Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(СчетчикОбщий, РольЧисловая, ВсегоРолей) Тогда
						//@skip-check typed-value-adding-to-untyped-collection
						ПредставлениеМассив.Добавить(Роль);
						Если Отбор <> Неопределено И СчетчикАтомарный <> Отбор Тогда
							СоответствуетОтбору = Ложь;	
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
				СчетчикАтомарный = СчетчикАтомарный + 1;
			КонецЦикла;	
			Если СоответствуетОтбору = Истина И ПредставлениеМассив.Количество() > 0 Тогда
				Результат.Вставить(СчетчикОбщий, СтрСоединить(ПредставлениеМассив, ", "));
			КонецЕсли;
		КонецЦикла;
	Иначе
		СчетчикАтомарный = 0;
		Для Каждого Роль Из АтомарныеРоли Цикл		
			Если РолиПоддерживаемыеМП.Найти(СчетчикАтомарный) <> Неопределено
				И (Отбор = Неопределено Или СчетчикАтомарный = Отбор) Тогда
				РольЧисловая = Pow(2, СчетчикАтомарный);
				Результат.Вставить(РольЧисловая, Роль);
			КонецЕсли;
			СчетчикАтомарный = СчетчикАтомарный + 1;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИнформацияПоТитулуМП(ДокументСсылка, РольУчастника) Экспорт
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") И РольУчастника = 1 Тогда
		Результат = Новый Структура;
		Результат.Вставить("ЗаголовокПолей", "По данным водителя");
		
		СобытияПривязки = Новый Массив;
		СобытияПривязки.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя")); 
		
		Результат.Вставить("СобытияПривязки", СобытияПривязки);
		
		Возврат Результат;	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция КартинкаПустаяФотография() Экспорт
	
	Возврат БиблиотекаКартинок.ПустаяФотографияЭПД.ПолучитьДвоичныеДанные();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает индексы поддерщиваемых МП ролей пользователя по массиву, возвращаемого методом АтомарныеРолиМП
Функция РолиПоддерживаемыеМП()
	
	Результат = Новый Массив;
	
	Если ДокументПоддерживаетсяМП(ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ")) = Истина Тогда
		//@skip-check typed-value-adding-to-untyped-collection
		Результат.Добавить(1);
		//@skip-check typed-value-adding-to-untyped-collection
		Результат.Добавить(2);
		//@skip-check typed-value-adding-to-untyped-collection
		Результат.Добавить(3);
	Иначе
		//@skip-check typed-value-adding-to-untyped-collection
		Результат.Добавить(4);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДокументПоддерживаетсяМП(ДокументЭПД)
	
	Результат = Ложь;
	
	Если ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная")
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭТрН") Тогда
		Результат = Истина;
	ИначеЕсли ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ") Тогда
		Результат = Ложь; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
