#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		// Проверки удаления записей
		Если ЗначениеЗаполнено(Отбор.ОрганизацияВладелец.Значение)
			И ЗначениеЗаполнено(Отбор.ОрганизацияПродавец.Значение)
			И РегистрыСведений.НастройкаПередачиТоваровМеждуОрганизациями.ЕстьНезакрытыеРезервы(Отбор.ОрганизацияВладелец.Значение, Отбор.ОрганизацияПродавец.Значение) Тогда
			
			ТекстСообщения = НСтр("ru = 'По указанным организациям есть остатки резервов для передачи.
			| Редактирование и удаление настроек возможно только под полными правами.'");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения , , , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Количество() > 0 И ЭтотОбъект[0].СпособПередачиТоваров <> Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	КонецЕсли;
	
	Если Количество() > 0 И ЭтотОбъект[0].СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.НеПередается Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидЦены");
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Договор");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		МассивНепроверяемыхРеквизитов);
		
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли