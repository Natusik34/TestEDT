
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСписок(Команда)
	
	Номера = СтрЗаменить(СтрокаДляОбработки, ",", ";");
	Номера = СтрЗаменить(Номера, Символы.ПС, ";");
	СписокНомеров = СтрРазделить(Номера, ";");
	
	СохранитьСписокНомеров(СписокНомеров);
	
	Оповестить("ОбновитьСписокНеобрабатываемыхНомеров");
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКнопка(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьСписокНомеров(СписокНомеров)

	НаборЗаписей = РегистрыСведений.НеобрабатываемыеТелефонныеНомера.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	ДатаДобавления = ТекущаяДатаСеанса();
	Автор = Пользователи.АвторизованныйПользователь();
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		Для Каждого Номер Из СписокНомеров Цикл 
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.НомерТелефонаАбонента = Номер;
			НоваяЗапись.Автор = Автор;
			НоваяЗапись.Дата = ДатаДобавления;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.НеобрабатываемыеТелефонныеНомера");
			ЭлементБлокировки.УстановитьЗначение("НомерТелефонаАбонента", Номер);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
		КонецЦикла;
		
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
